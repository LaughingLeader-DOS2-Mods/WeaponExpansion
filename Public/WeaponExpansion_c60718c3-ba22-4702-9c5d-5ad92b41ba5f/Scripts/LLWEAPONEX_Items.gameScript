INIT

EVENTS

EVENT LLWEAPONEX_DualShields_SetLevel
VARS
	ITEM:_Shield
	FLOAT:_LevelF
	INT:_Level
	FIXEDSTRING:_Rarity
	STRING:_Str
ON
	OnItemEvent(_Shield, "LLWEAPONEX_Commands_SetDualShieldLevel")
ACTIONS
IF "c1"
	ItemGetStat(_LevelF, _Shield, Level)
THEN
	Cast(_Level, _LevelF)
	SetVar(_Shield, "LeaderLib_ItemLevel", _Level)
	ItemEvent(_Shield, "LLWEAPONEX_Commands_LevelUpCombatShield")
ENDIF
IF "!c1&c2"
	GetVar(_Rarity, _Shield, "LLWEAPONEX_Rarity")
	ItemGetItemType(_Shield, _Rarity)
THEN
	SetVar(_Shield, "LLWEAPONEX_Rarity", _Rarity)
	Print(_Str, "[LLWEAPONEX:DualShields:ItemGetItemType] Dual Shields rarity is [1]", _Rarity)
	SetVar(_Shield, "LeaderLog_Message", _Str)
	ItemEvent(_Shield, "LeaderLog_Commands_Log")
	ItemEvent(_Shield, "LLWEAPONEX_DualShields_RaritySet")
ENDIF

EVENT LLWEAPONEX_DualShields_GetCombatShield
VARS
	ITEM:_Shield
	ITEM:_CombatShield
ON
	OnItemEvent(_Shield, "LLWEAPONEX_DualShields_FindCombatShield")
ACTIONS
IF "c1"
	ItemGetFromInventory(_CombatShield, _Shield, "", "LLWEAPONEX_CombatShield")
THEN
	SetVar(_Shield, "LLWEAPONEX_CombatShield", _CombatShield)
	ItemEvent(_Shield, "LLWEAPONEX_DualShields_CombatShieldFound")
ELSE
	ItemEvent(_Shield, "LLWEAPONEX_DualShields_CombatShieldNotFound")
ENDIF

EVENT LLWEAPONEX_Blunderbuss_Explode_Position
VARS
	CHARACTER:_Character
	FLOAT3:_Pos
	FLOAT:_LevelF
	INT:_Level
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_Blunderbuss_ExplodeAtPosition")
ACTIONS
IF "c1&c2&!c3"
	CharacterGetStat(_LevelF, _Character, Level)
	GetVar(_Pos, _Character, "LLWEAPONEX_Blunderbuss_AttackPosition")
	IsEqual(_Pos, null)
THEN
	Cast(_Level, _LevelF)
	ExplodeAt(_Pos, Projectile_LLWEAPONEX_Blunderbuss_Shot_Explode, _Level, _Character)
ENDIF

EVENT LLWEAPONEX_Blunderbuss_Explode_Character
VARS
	CHARACTER:_Character
	CHARACTER:_Target
	FLOAT:_LevelF
	INT:_Level
ON
	OnCharacterCharacterEvent(_Character, _Target, "LLWEAPONEX_Blunderbuss_ExplodeProjectile")
ACTIONS
	Set(_Level, -1)
IF "c1"
	CharacterGetStat(_LevelF, _Character, Level)
THEN
	Cast(_Level, _LevelF)
ENDIF
	ExplodeAt(_Target, Projectile_LLWEAPONEX_Blunderbuss_Shot_Explode, _Level, _Character)

EVENT LLWEAPONEX_Blunderbuss_Explode_Item
VARS
	CHARACTER:_Character
	ITEM:_Target
	FLOAT:_LevelF
	INT:_Level
ON
	OnCharacterItemEvent(_Character, _Target, "LLWEAPONEX_Blunderbuss_ExplodeProjectile")
ACTIONS
	Set(_Level, -1)
IF "c1"
	CharacterGetStat(_LevelF, _Character, Level)
THEN
	Cast(_Level, _LevelF)
ENDIF
	ExplodeAt(_Target, Projectile_LLWEAPONEX_Blunderbuss_Shot_Explode, _Level, _Character)

EVENT LLWEAPONEX_SoulHarvester_DamageBonus
VARS
	CHARACTER:_Target
	LIST<STATUS>:_RemoveList
	STATUS:_Result
	INT:_Turns
	INT:_TurnsLeft
ON
	FetchCharacterApplyStatusData(_Target, LLWEAPONEX_SOULHARVEST_DAMAGE_BONUS_APPLY)
ACTIONS
	Set(_Result, null)
	Set(_Turns, -1)
IF "c1"
	CharacterHasStatus(_Target, LLWEAPONEX_SOULHARVEST_DAMAGE_BONUS1)
THEN
	Set(_Result, LLWEAPONEX_SOULHARVEST_DAMAGE_BONUS2)
	ListAdd(_RemoveList, LLWEAPONEX_SOULHARVEST_DAMAGE_BONUS1)
ELIF "c1"
	CharacterHasStatus(_Target, LLWEAPONEX_SOULHARVEST_DAMAGE_BONUS2)
THEN
	Set(_Result, LLWEAPONEX_SOULHARVEST_DAMAGE_BONUS3)
	ListAdd(_RemoveList, LLWEAPONEX_SOULHARVEST_DAMAGE_BONUS2)
ELIF "c1"
	CharacterHasStatus(_Target, LLWEAPONEX_SOULHARVEST_DAMAGE_BONUS3)
THEN
	Set(_Result, LLWEAPONEX_SOULHARVEST_DAMAGE_BONUS4)
	ListAdd(_RemoveList, LLWEAPONEX_SOULHARVEST_DAMAGE_BONUS3)
ELIF "c1"
	CharacterHasStatus(_Target, LLWEAPONEX_SOULHARVEST_DAMAGE_BONUS4)
THEN
	Set(_Result, LLWEAPONEX_SOULHARVEST_DAMAGE_BONUS5)
	ListAdd(_RemoveList, LLWEAPONEX_SOULHARVEST_DAMAGE_BONUS4)
ELIF "!c1"
	CharacterHasStatus(_Target, LLWEAPONEX_SOULHARVEST_DAMAGE_BONUS5)
THEN
	Set(_Result, LLWEAPONEX_SOULHARVEST_DAMAGE_BONUS1)
ENDIF
IF "!c1"
	IsEqual(_Result, null)
THEN
	CharacterPlayEffect(_Target, "LLWEAPONEX_FX_Status_SoulHarvest_Impact_01", "Dummy_OverheadFX")
ENDIF
	RETURN(_RemoveList, _Result, _Turns)
	
EVENT LLWEAPONEX_AnvilMace_CreateTimerName
VARS
	CHARACTER:_Char
	FLOAT3:_Pos
	STRING:_TimerName
	FLOAT:_x
	FLOAT:_y
	FLOAT:_z
	//FIXEDSTRING:_Debug
ON
	OnCharacterEvent(_Char, "LLWEAPONEX_AnvilMace_CreateTimerName")
ACTIONS
IF "c1&!c2&c3&c4&c5"
	GetVar(_Pos, _Char, "LLWEAPONEX_AnvilMace_AttackPosition")
	IsEqual(_Pos, null)
	GetX(_Pos, _x)
	GetY(_Pos, _y)
	GetZ(_Pos, _z)
THEN
	//Print(_TimerName, "LLWEAPONEX_Timers_AnvilMaceImpact_[1][2][3]", _x, _y, _z)
	Print(_TimerName, "LLWEAPONEX_Timers_AnvilMaceImpact_[1]", _Pos)
	SetVar(_Char, "LLWEAPONEX_AnvilMace_TimerName", _TimerName)
	CharacterEvent(_Char, "LLWEAPONEX_AnvilMace_TimerNameCreated")
	//Cast(_Debug, _TimerName)
	//StatusText(_Char, _Debug)
ENDIF	

EVENT LLWEAPONEX_AnvilMace_Explode_Position
VARS
	CHARACTER:_Char
	FLOAT3:_Pos
	FLOAT:_LevelF
	INT:_Level
ON
	OnCharacterEvent(_Char, "LLWEAPONEX_AnvilMace_ExplodeAtPosition")
ACTIONS
IF "c1&c2&!c3"
	CharacterGetStat(_LevelF, _Char, Level)
	GetVar(_Pos, _Char, "LLWEAPONEX_AnvilMace_AttackPosition")
	IsEqual(_Pos, null)
THEN
	Cast(_Level, _LevelF)
	ExplodeAt(_Pos, Projectile_LLWEAPONEX_AnvilMace_GroundImpact, _Level, _Char)
	//ShootWorldProjectile(Projectile_LLWEAPONEX_AnvilMace_GroundImpact, _Char, _Pos, {0;-1.0;0}, _Level)
ENDIF

EVENT LLWEAPONEX_BattleBook_SpellScroll_Explode_Position
VARS
	CHARACTER:_Char
	FLOAT3:_Pos
	FLOAT:_LevelF
	INT:_Level
	INT:_Shoot
	SKILL:_Projectile
	FIXEDSTRING:_ProjectileStr
	STRING:_Str
	FIXEDSTRING:_Msg
ON
	OnCharacterEvent(_Char, "LLWEAPONEX_BattleBooks_SpellScroll_ExplodeSpellAtPosition")
ACTIONS
	GetPosition(_Char, _Pos)
	Set(_Shoot, 0)
	Set(_Level, 1)
	Set(_Projectile, Projectile_LLWEAPONEX_BattleBooks_SpellScroll_FlamingDaggers)
IF "c1&c2&c3&c4"
	CharacterGetStat(_LevelF, _Char, Level)
	GetVar(_Pos, _Char, "LLWEAPONEX_BattleBooks_SpellScroll_AttackPosition")
	GetVar(_Projectile, _Char, "LLWEAPONEX_BattleBooks_SpellScroll_Projectile")
	GetVar(_Shoot, _Char, "LLWEAPONEX_BattleBooks_SpellScroll_ShootProjectile")
THEN
ENDIF
	Cast(_Level, _LevelF)
IF "c1"
	IsEqual(_Projectile, Projectile_LLWEAPONEX_BattleBooks_SpellScroll_TotemFromSurface)
THEN
	Set(_Shoot, 1)
	IF "c1|c2|c3|c4|c5|c6|c7|c8"
		ContainsSurface(_Pos, 1.0, SurfaceFire)
		ContainsSurface(_Pos, 1.0, SurfaceFireCloud)
		ContainsSurface(_Pos, 1.0, SurfaceFireBlessed)
		ContainsSurface(_Pos, 1.0, SurfaceFireCursed)
		ContainsSurface(_Pos, 1.0, SurfaceFirePurified)
		ContainsSurface(_Pos, 1.0, SurfaceFireCloudBlessed)
		ContainsSurface(_Pos, 1.0, SurfaceFireCloudCursed)
		ContainsSurface(_Pos, 1.0, SurfaceFireCloudPurified)
	THEN
		PlayEffectAt(_Pos, "RS3_FX_Skills_Totem_Impact_Summon_Fire_01")
		Set(_Projectile, Projectile_LLWEAPONEX_BattleBooks_SpellScroll_TotemFromSurface_Fire)
	ELIF "c1|c2|c3|c4|c5|c6|c7|c8|c9"
		ContainsSurface(_Pos, 1.0, SurfaceWaterFrozen)
		ContainsSurface(_Pos, 1.0, SurfaceWaterFrozenBlessed)
		ContainsSurface(_Pos, 1.0, SurfaceWaterFrozenCursed)
		ContainsSurface(_Pos, 1.0, SurfaceWaterFrozenPurified)
		ContainsSurface(_Pos, 1.0, SurfaceBloodFrozen)
		ContainsSurface(_Pos, 1.0, SurfaceBloodFrozenBlessed)
		ContainsSurface(_Pos, 1.0, SurfaceBloodFrozenCursed)
		ContainsSurface(_Pos, 1.0, SurfaceBloodFrozenPurified)
		ContainsSurface(_Pos, 1.0, SurfaceFrostCloud)
	THEN
		PlayEffectAt(_Pos, "RS3_FX_Skills_Totem_Impact_Summon_Ice_01")
		Set(_Projectile, Projectile_LLWEAPONEX_BattleBooks_SpellScroll_TotemFromSurface_Water)
	ELIF "c1|c2|c3|c4|c5|c6|c7|c8|c9"
		ContainsSurface(_Pos, 1.0, SurfaceWater)
		ContainsSurface(_Pos, 1.0, SurfaceWaterCloud)
		ContainsSurface(_Pos, 1.0, SurfaceWaterBlessed)
		ContainsSurface(_Pos, 1.0, SurfaceWaterCursed)
		ContainsSurface(_Pos, 1.0, SurfaceWaterPurified)
		ContainsSurface(_Pos, 1.0, SurfaceWaterCloudBlessed)
		ContainsSurface(_Pos, 1.0, SurfaceWaterCloudCursed)
		ContainsSurface(_Pos, 1.0, SurfaceWaterCloudPurified)
		ContainsSurface(_Pos, 1.0, SurfaceDeepwater)
	THEN
		PlayEffectAt(_Pos, "RS3_FX_Skills_Totem_Impact_Summon_Water_01")
		Set(_Projectile, Projectile_LLWEAPONEX_BattleBooks_SpellScroll_TotemFromSurface_Water)
	ELIF "c1|c2|c3|c4|c5|c6|c7|c8|c9|c10|c11|c12|c13|c14|c15"
		ContainsSurface(_Pos, 1.0, SurfaceWaterElectrified)
		ContainsSurface(_Pos, 1.0, SurfaceWaterCloudElectrified)
		ContainsSurface(_Pos, 1.0, SurfaceWaterElectrifiedBlessed)
		ContainsSurface(_Pos, 1.0, SurfaceWaterElectrifiedCursed)
		ContainsSurface(_Pos, 1.0, SurfaceWaterElectrifiedPurified)
		ContainsSurface(_Pos, 1.0, SurfaceWaterCloudElectrifiedBlessed)
		ContainsSurface(_Pos, 1.0, SurfaceWaterCloudElectrifiedCursed)
		ContainsSurface(_Pos, 1.0, SurfaceWaterCloudElectrifiedPurified)
		ContainsSurface(_Pos, 1.0, SurfaceBloodElectrified)
		ContainsSurface(_Pos, 1.0, SurfaceBloodElectrifiedBlessed)
		ContainsSurface(_Pos, 1.0, SurfaceBloodElectrifiedCursed)
		ContainsSurface(_Pos, 1.0, SurfaceBloodElectrifiedPurified)
		ContainsSurface(_Pos, 1.0, SurfaceBloodCloudElectrifiedBlessed)
		ContainsSurface(_Pos, 1.0, SurfaceBloodCloudElectrifiedCursed)
		ContainsSurface(_Pos, 1.0, SurfaceBloodCloudElectrifiedPurified)
	THEN
		PlayEffectAt(_Pos, "RS3_FX_Skills_Totem_Impact_Summon_Lightning_01")
		Set(_Projectile, Projectile_LLWEAPONEX_BattleBooks_SpellScroll_TotemFromSurface_Air)
	ELIF "c1|c2|c3|c4"
		ContainsSurface(_Pos, 1.0, SurfaceOil)
		ContainsSurface(_Pos, 1.0, SurfaceOilBlessed)
		ContainsSurface(_Pos, 1.0, SurfaceOilCursed)
		ContainsSurface(_Pos, 1.0, SurfaceOilPurified)
	THEN
		PlayEffectAt(_Pos, "RS3_FX_Skills_Totem_Impact_Summon_Oil_01")
		Set(_Projectile, Projectile_LLWEAPONEX_BattleBooks_SpellScroll_TotemFromSurface_Oil)
	ELIF "c1|c2|c3|c4|c5|c6|c7|c8"
		ContainsSurface(_Pos, 1.0, SurfacePoison)
		ContainsSurface(_Pos, 1.0, SurfacePoisonCloud)
		ContainsSurface(_Pos, 1.0, SurfacePoisonBlessed)
		ContainsSurface(_Pos, 1.0, SurfacePoisonCursed)
		ContainsSurface(_Pos, 1.0, SurfacePoisonPurified)
		ContainsSurface(_Pos, 1.0, SurfacePoisonCloudBlessed)
		ContainsSurface(_Pos, 1.0, SurfacePoisonCloudCursed)
		ContainsSurface(_Pos, 1.0, SurfacePoisonCloudPurified)
	THEN
		PlayEffectAt(_Pos, "RS3_FX_Skills_Totem_Impact_Summon_Poison_01")
		Set(_Projectile, Projectile_LLWEAPONEX_BattleBooks_SpellScroll_TotemFromSurface_Poison)
	ELIF "c1|c2|c3|c4|c5|c6|c7|c8|c9|c10|c11|c12"
		ContainsSurface(_Pos, 1.0, SurfaceBlood)
		ContainsSurface(_Pos, 1.0, SurfaceBloodCloud)
		ContainsSurface(_Pos, 1.0, SurfaceBloodBlessed)
		ContainsSurface(_Pos, 1.0, SurfaceBloodCursed)
		ContainsSurface(_Pos, 1.0, SurfaceBloodPurified)
		ContainsSurface(_Pos, 1.0, SurfaceBloodCloudBlessed)
		ContainsSurface(_Pos, 1.0, SurfaceBloodCloudCursed)
		ContainsSurface(_Pos, 1.0, SurfaceBloodCloudPurified)
		ContainsSurface(_Pos, 1.0, SurfaceBloodFrozen)
		ContainsSurface(_Pos, 1.0, SurfaceBloodFrozenBlessed)
		ContainsSurface(_Pos, 1.0, SurfaceBloodFrozenCursed)
		ContainsSurface(_Pos, 1.0, SurfaceBloodFrozenPurified)
	THEN
		PlayEffectAt(_Pos, "RS3_FX_Skills_Totem_Impact_Summon_Blood_01")
		Set(_Projectile, Projectile_LLWEAPONEX_BattleBooks_SpellScroll_TotemFromSurface_Blood)
	ELIF "c1"
		ContainsSurface(_Pos, 1.0, SurfaceSource)
	THEN
		PlayEffectAt(_Pos, "RS3_FX_Skills_Totem_Impact_Summon_Default_01")
		Set(_Projectile, Projectile_LLWEAPONEX_BattleBooks_SpellScroll_TotemFromSurface_Source)
	ELSE
		PlayEffectAt(_Pos, "RS3_FX_Skills_Totem_Impact_Summon_Default_01")
		Set(_Projectile, Projectile_LLWEAPONEX_BattleBooks_SpellScroll_TotemFromSurface)
	ENDIF
ENDIF
/*
IF "!c1"
	IsProjectileSkill(_Projectile)
THEN
	Set(_Projectile, Projectile_LLWEAPONEX_BattleBooks_SpellScroll_FlamingDaggers)
ENDIF
*/
IF "c1"
	IsEqual(_Shoot, 0)
THEN
	ExplodeAt(_Pos, _Projectile, _Level, _Char)
ELSE
	ShootLocalProjectileAt(_Projectile, _Char, {0;4;0}, _Pos, _Level, _Char)
ENDIF
	CharacterEvent(_Char, "LLWEAPONEX_BattleBooks_SpellScroll_SpellExplodedAtPosition")