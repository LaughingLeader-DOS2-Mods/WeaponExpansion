INIT

EVENTS

EVENT LLWEAPONEX_DualShields_SetLevel
VARS
	ITEM:_Shield
	FLOAT:_LevelF
	INT:_Level
	FIXEDSTRING:_Rarity
	STRING:_Str
ON
	OnItemEvent(_Shield, "LLWEAPONEX_Commands_SetDualShieldLevel")
ACTIONS
IF "c1"
	ItemGetStat(_LevelF, _Shield, Level)
THEN
	Cast(_Level, _LevelF)
	SetVar(_Shield, "LeaderLib_ItemLevel", _Level)
	ItemEvent(_Shield, "LLWEAPONEX_Commands_LevelUpCombatShield")
ENDIF
IF "!c1&c2"
	GetVar(_Rarity, _Shield, "LLWEAPONEX_Rarity")
	ItemGetItemType(_Shield, _Rarity)
THEN
	SetVar(_Shield, "LLWEAPONEX_Rarity", _Rarity)
	Print(_Str, "[LLWEAPONEX:DualShields:ItemGetItemType] Dual Shields rarity is [1]", _Rarity)
	SetVar(_Shield, "LeaderLog_Message", _Str)
	ItemEvent(_Shield, "LeaderLog_Commands_Log")
	ItemEvent(_Shield, "LLWEAPONEX_DualShields_RaritySet")
ENDIF

EVENT LLWEAPONEX_DualShields_GetCombatShield
VARS
	ITEM:_Shield
	ITEM:_CombatShield
ON
	OnItemEvent(_Shield, "LLWEAPONEX_DualShields_FindCombatShield")
ACTIONS
IF "c1"
	ItemGetFromInventory(_CombatShield, _Shield, "", "LLWEAPONEX_CombatShield")
THEN
	SetVar(_Shield, "LLWEAPONEX_CombatShield", _CombatShield)
	ItemEvent(_Shield, "LLWEAPONEX_DualShields_CombatShieldFound")
ELSE
	ItemEvent(_Shield, "LLWEAPONEX_DualShields_CombatShieldNotFound")
ENDIF

EVENT LLWEAPONEX_Blunderbuss_Explode
VARS
	CHARACTER:_Character
	FLOAT3:_Pos
	FLOAT:_LevelF
	INT:_Level
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_Blunderbuss_ExplodeAtPosition")
ACTIONS
IF "c1&c2&!c3"
	CharacterGetStat(_LevelF, _Character, Level)
	GetVar(_Pos, _Character, "LLWEAPONEX_Blunderbuss_AttackPosition")
	IsEqual(_Pos, null)
THEN
	Cast(_Level, _LevelF)
	ExplodeAt(_Pos, Projectile_LLWEAPONEX_Blunderbuss_Shot_Explode, _Level, _Character)
ENDIF

EVENT LLWEAPONEX_Blunderbuss_CannonLaunch_Damage_Character
VARS
	CHARACTER:_Player
	CHARACTER:_Target
	FLOAT:_LevelF
	INT:_Level
ON
	OnCharacterCharacterEvent(_Player, _Target, "LLWEAPONEX_Bluderbuss_CannonLaunch_OnHit")
ACTIONS
IF "c1"
	CharacterGetStat(_LevelF, _Player, Level)
THEN
	Cast(_Level, _LevelF)
ELSE
	Set(_Level, -1)
ENDIF
	ExplodeAt(_Target, Projectile_LLWEAPONEX_CannonLaunch_PassThrough_Explode, _Level, _Player)

EVENT LLWEAPONEX_Blunderbuss_CannonLaunch_Damage_Item
VARS
	CHARACTER:_Player
	ITEM:_Target
	FLOAT:_LevelF
	INT:_Level
ON
	OnCharacterItemEvent(_Player, _Target, "LLWEAPONEX_Bluderbuss_CannonLaunch_OnHit")
ACTIONS
IF "c1"
	CharacterGetStat(_LevelF, _Player, Level)
THEN
	Cast(_Level, _LevelF)
ELSE
	Set(_Level, -1)
ENDIF
	ExplodeAt(_Target, Projectile_LLWEAPONEX_CannonLaunch_PassThrough_Explode, _Level, _Player)