INIT
	ITEM:__Me
	//External settings
	EXTERN SKILL:%LLWEAPONEX_Mine_Skill = Projectile_LLWEAPONEX_RemoteMine_Explosive
	EXTERN SKILL:%LLWEAPONEX_Mine_Skill_NoWeapon = Projectile_LLWEAPONEX_RemoteMine_Explosive
	EXTERN STRING:%LLWEAPONEX_Mine_ActiveLoopEffect = null
	EXTERN INT:%LLWEAPONEX_Mine_UseSelfAsSource = 0
	
	//Dynamic/Important Vars
	CHARACTER:%LLWEAPONEX_Mine_Owner = null
	ITEM:%LLWEAPONEX_Mine_Inventory = null
	
	//State Vars
	INT:%LLWEAPONEX_Mine_Exploding = 0
	INT:%LLWEAPONEX_Mine_Animating = 0
	
	//Temp Vars
	INT64:%LLWEAPONEX_Mine_LoopFXHandle = null
	FLOAT3:%LLWEAPONEX_Mine_ExplodePos = null
EVENTS

EVENT LLWEAPONEX_RemoteMine_Destroying
ON
	OnItemDestroying(__Me)
ACTIONS
IF "!c1&!c2&!c3"
	ItemIsInInventory(__Me)
	HasFlag(__Me, "LLWEAPONEX_RemoteMine_Detonated")
	IsEqual(%LLWEAPONEX_Mine_Exploding, 1)
THEN
	ItemApplyStatus(__Me, LLWEAPONEX_REMOTEMINE_DETONATE, 0)
ENDIF

EVENT LLWEAPONEX_RemoteMine_OnPickup
ON
	//OnPickupItem(_, __Me)
	OnCharacterItemEvent(_, __Me, "LLWEAPONEX_RemoteMine_OnPrePickedUp")
ACTIONS
	//SetPriority("LLWEAPONEX_RemoteMine_InWorld", 0)
	DelayReaction("LLWEAPONEX_RemoteMine_InWorld", 5.0)
	ClearTag(__Me, "LLWEAPONEX_RemoteMine_Ready")
IF "!c1"
	IsEqual(%LLWEAPONEX_Mine_LoopFXHandle, null)
THEN
	StopLoopEffect(%LLWEAPONEX_Mine_LoopFXHandle)
	Set(%LLWEAPONEX_Mine_LoopFXHandle, INT64:null)
ENDIF

BEHAVIOUR

REACTION LLWEAPONEX_RemoteMine_InWorld,998
USAGE ALL
CHECK "c1&!c2&!c3&!c4&!c5"
	IsActive(__Me)
	ItemIsInInventory(__Me)
	IsTagged(__Me, "LLWEAPONEX_RemoteMine_Ready")
	IsEqual(%LLWEAPONEX_Mine_Exploding, 1)
	ItemIsDestroyed(__Me)
ACTIONS
	SetTag(__Me, "LLWEAPONEX_RemoteMine_Ready")
IF "!c1&!c2&c3"
	IsEqual(%LLWEAPONEX_Mine_ActiveLoopEffect, null)
	IsEqual(%LLWEAPONEX_Mine_ActiveLoopEffect, "")
	IsEqual(%LLWEAPONEX_Mine_LoopFXHandle, null)
THEN
	ItemPlayLoopEffect(%LLWEAPONEX_Mine_LoopFXHandle, __Me, %LLWEAPONEX_Mine_ActiveLoopEffect)
ENDIF

REACTION LLWEAPONEX_RemoteMine_PlayDetonateAnim,999
USAGE ALL
CHECK "(c1|c2)&!c3"
	IsEqual(%LLWEAPONEX_Mine_Exploding, 1)
	ItemHasStatus(__Me, LLWEAPONEX_REMOTEMINE_DETONATE)
	IsEqual(%LLWEAPONEX_Mine_Animating, 1)
ACTIONS
	ItemPlayAnimation("detonate")
	Set(%LLWEAPONEX_Mine_Animating, 1)