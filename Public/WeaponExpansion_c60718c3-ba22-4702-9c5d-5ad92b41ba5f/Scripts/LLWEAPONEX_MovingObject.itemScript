INIT
	ITEM:__Me
	EXTERN STRING:%LLWEAPONEX_InitEvent = null
	EXTERN STRING:%LLWEAPONEX_LandEvent = null
	EXTERN STRING:%LLWEAPONEX_TickEvent = null
	EXTERN FLOAT:%LLWEAPONEX_TimerTickSpeed = 0.001
	
	EXTERN FLOAT:%LLWEAPONEX_Radius = 1.0
	EXTERN FLOAT:%LLWEAPONEX_Lifetime = 6.0
	EXTERN FLOAT:%LLWEAPONEX_InitEventDelay = 0.025
	
	EXTERN STRING:%LLWEAPONEX_Effect = null
	EXTERN FLOAT:%LLWEAPONEX_EffectTimerSpeed = 0.001
	EXTERN INT:%LLWEAPONEX_EffectTimerRepeatCount = -1
	EXTERN SKILL:%LLWEAPONEX_ProjectileSkill=null
	//CHARACTER:%LLWEAPONEX_Caster = null
EVENTS
EVENT LLWEAPONEX_InitMovingObject
ON
	OnInit()
ACTIONS
IF "!c1&!c2"
	IsEqual(%LLWEAPONEX_InitEvent, null)
	IsEqual(%LLWEAPONEX_InitEvent, "")
THEN
	IF "c1"
		IsEqual(%LLWEAPONEX_InitEventDelay, 0)
	THEN
		ItemEvent(__Me, %LLWEAPONEX_InitEvent)
	ELSE
		StartTimer("LLWEAPONEX_Timers_MovingObject_InitEvent", %LLWEAPONEX_InitEventDelay, 0)
	ENDIF
ENDIF
IF "c1"
	ItemGetSkillId(__Me,%LLWEAPONEX_ProjectileSkill)
THEN
ENDIF
IF "!c1"
	IsEqual(%LLWEAPONEX_Effect, null)
THEN
	StartTimer("LLWEAPONEX_MovingObject_PlayEffect", %LLWEAPONEX_EffectTimerSpeed, 0)
ENDIF

IF "!c1&!c2&c3"
	IsEqual(%LLWEAPONEX_TickEvent, null)
	IsEqual(%LLWEAPONEX_TickEvent, "")
	IsGreaterThen(%LLWEAPONEX_TimerTickSpeed, 0)
THEN
	StartTimer("LLWEAPONEX_MovingObject_Tick", %LLWEAPONEX_TimerTickSpeed, -1)
ENDIF

EVENT LLWEAPONEX_MovingObject_Tick
ON
	OnTimer("LLWEAPONEX_MovingObject_Tick")
ACTIONS
	ItemEvent(__Me, %LLWEAPONEX_TickEvent)

EVENT LLWEAPONEX_MovingObject_DelayedInit
ON
	OnTimer("LLWEAPONEX_Timers_MovingObject_InitEvent")
	OnItemStatusAttempt(__Me, SUMMONING)
ACTIONS
	StopTimer("LLWEAPONEX_Timers_MovingObject_InitEvent")
	ItemEvent(__Me, %LLWEAPONEX_InitEvent)

EVENT LLWEAPONEX_MovingObject_InitialEffect
ON
	OnTimer("LLWEAPONEX_MovingObject_PlayEffect")
ACTIONS
IF "!c1"
	ItemIsDestroyed(__Me)
THEN
	PlayEffectAt(__Me, %LLWEAPONEX_Effect)
ENDIF

EVENT LLWEAPONEX_MovingObject_Destroyed
ON
	OnItemDestroyed(__Me)
	OnFunction("LLWEAPONED_MovingObject_Landed")
ACTIONS
	StopTimer("LLWEAPONEX_MovingObject_Tick")
	StopTimer("LLWEAPONEX_MovingObject_PlayEffect")

EVENT LLWEAPONEX_MovingObject_Land
VARS 
	INT:_HitObstacle
	CHARACTER:_Caster	
ON
	OnGrenadeLand(_HitObstacle, _Caster)
ACTIONS
	CallFunction("LLWEAPONED_MovingObject_Landed")
	ItemEvent(__Me, "LeaderLib_Commands_CancelObjectTimer")
	
IF "!c1"
	IsEqual(%LLWEAPONEX_ProjectileSkill, null)
THEN
	ExplodeAt(__Me, %LLWEAPONEX_ProjectileSkill, -1, _Caster)
ENDIF
IF "!c1&!c2"
	IsEqual(%LLWEAPONEX_LandEvent, null)
	IsEqual(%LLWEAPONEX_LandEvent, "")
THEN
	CharacterItemEvent(_Caster, __Me, %LLWEAPONEX_LandEvent)
ENDIF