INIT
	FLOAT:%LLWEAPONEX_Greatbow_KnockbackMaxStart = 12.0 // Should round down to ~10 max

EVENTS

EVENT LLWEAPONEX_Greatbows_CushionForce_Start
VARS
	CHARACTER:_Target
	CHARACTER:_Source
	INT:_ForceIndex
	SKILL:_ForceSkill
	STRING:_DebugMessage
	FIXEDSTRING:_DebugMessageF
	INT:_Level
	FLOAT:_LevelF
ON
	//OnCharacterStatusApplied(_Target, LLWEAPONEX_GREATBOW_CUSHION_FORCE)
	OnCharacterCharacterEvent(_Target, _Source, "LLWEAPONEX_Greatbow_CushionForce_StartLaunch")
ACTIONS
	Set(_ForceIndex, 0)
	Set(_ForceSkill, Projectile_LeaderLib_Force4)
IF "c1"
	GetVar(_ForceIndex, _Target, "LLWEAPONEX_CushionForce_ForceIndex")
THEN
ENDIF
	IF "c1"
		IsEqual(_ForceIndex, 0)
	THEN
		Set(_ForceSkill, Projectile_LeaderLib_Force4)
	ELIF "c1"
		IsEqual(_ForceIndex, 1)
	THEN
		Set(_ForceSkill, Projectile_LeaderLib_Force5)
	ELIF "c1"
		IsEqual(_ForceIndex, 2)
	THEN
		Set(_ForceSkill, Projectile_LeaderLib_Force6)
	ELIF "c1"
		IsEqual(_ForceIndex, 3)
	THEN
		Set(_ForceSkill, Projectile_LeaderLib_Force7)
	ELIF "c1"
		IsEqual(_ForceIndex, 4)
	THEN
		Set(_ForceSkill, Projectile_LeaderLib_Force8)
	ELIF "c1"
		IsEqual(_ForceIndex, 5)
	THEN
		Set(_ForceSkill, Projectile_LeaderLib_Force9)
	ELIF "c1"
		IsEqual(_ForceIndex, 6)
	THEN
		Set(_ForceSkill, Projectile_LeaderLib_Force10)
	ELIF "c1"
		IsEqual(_ForceIndex, 7)
	THEN
		Set(_ForceSkill, Projectile_LeaderLib_Force11)
	ELIF "c1"
		IsEqual(_ForceIndex, 8)
	THEN
		Set(_ForceSkill, Projectile_LeaderLib_Force12)
	ENDIF
	ShootLocalProjectileAt(_ForceSkill, _Source, FLOAT3:{0;3.0;0}, _Target, 1, _Source)
	/*
	Print(_DebugMessage, "[LLWEAPONEX:Greatbows:CushionForce] Distance([1] - [2] = [3] | Int([4])) = Skill [5]", %LLWEAPONEX_Greatbow_KnockbackMaxStart, _Dist, _ForceDist, _ForceDistInt, _ForceSkill)
	SetVar(_GreatbowCaster, "LeaderLog_Type", STRING:"Debug")
	SetVar(_GreatbowCaster, "LeaderLog_Message", _DebugMessage)
	CharacterEvent(_GreatbowCaster, "LeaderLog_Commands_Log")
	*/

EVENT LLWEAPONEX_Greatbows_CushionForce_DamageCheck_Characters
VARS
	CHARACTER:_Target
	CHARACTER:_AuraHost
	CHARACTER:_GreatbowCaster
	INT:_Turns
ON
	OnCharacterStatusApplied(_Target, LLWEAPONEX_GREATBOW_CUSHION_DAMAGE_CHECK)
ACTIONS
IF "c1&c2&c3"
	CharacterGetStatusSourceCharacter(_Target, LLWEAPONEX_GREATBOW_CUSHION_DAMAGE_CHECK, _AuraHost)
	CharacterGetStatusSourceCharacter(_AuraHost, LLWEAPONEX_GREATBOW_CUSHION_FORCE, _GreatbowCaster)
	CharacterIsEnemy(_Target, _GreatbowCaster)
THEN
	CharacterApplyStatus(_Target, LLWEAPONEX_GREATBOW_CUSHION_DAMAGE, 0, 0, _GreatbowCaster)
	//Purely for the animation
	IF "!c1"
		CharacterHasStatus(_Target, KNOCKED_DOWN)
	THEN
		//Play the knocked down animation
		CharacterEvent(_Target, "GEN_FallAndGetUp")
	ENDIF
ENDIF

EVENT LLWEAPONEX_Greatbows_CushionForce_DamageCheck_Items
VARS
	ITEM:_Target
	CHARACTER:_AuraHost
	CHARACTER:_GreatbowCaster
	INT:_Turns
ON
	OnItemStatus(_Target, LLWEAPONEX_GREATBOW_CUSHION_DAMAGE_CHECK)
ACTIONS
IF "!c1&c2&c3"
	IsStoryItem(_Target)
	ItemGetStatusSourceCharacter(_Target, LLWEAPONEX_GREATBOW_CUSHION_DAMAGE_CHECK, _AuraHost)
	CharacterGetStatusSourceCharacter(_AuraHost, LLWEAPONEX_GREATBOW_CUSHION_FORCE, _GreatbowCaster)
THEN
	ItemApplyStatus(_Target, LLWEAPONEX_GREATBOW_CUSHION_DAMAGE_ITEM, 0, 0, _GreatbowCaster)
	//ExplodeAt(_Target, Projectile_LLWEAPONEX_Status_Greatbow_CushionDamage, -1, _GreatbowCaster)
ENDIF

EVENT LLWEAPONEX_Greatbows_PlayAttackFX
VARS
	CHARACTER:_Character
	INT64:_FXHandle
	STRING:_Effect
	FIXEDSTRING:_Bone
	STRING:_Race
	INT:_EffectDuration
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_Greatbow_PlayAttackFX")
ACTIONS
/*
IF "c1&!c2"
	GetVar(_FXHandle, _Character, "LLWEAPONEX_Greatbow_AttackFXHandle")
	IsEqual(_FXHandle, null)
THEN
	StopLoopEffect(_FXHandle, 1)
ENDIF
*/
IF "c1&c2&c3"
	GetVar(_Effect, _Character, "LLWEAPONEX_Greatbow_AttackEffectName")
	GetVar(_Bone, _Character, "LLWEAPONEX_Greatbow_AttackEffectBone")
	GetVar(_Race, _Character, "LLWEAPONEX_Greatbow_RaceTag")
THEN
	//We set the duration here because it's 100% faster to test than rebuilding + reloading the story every time
	IF "c1"
		IsEqual(_Race, "REALLY_DWARF")
	THEN
		Set(_EffectDuration, INT:500)
	ELIF "c1"
		IsEqual(_Race, "REALLY_ELF")
	THEN
		Set(_EffectDuration, INT:500)
	ELIF "c1"
		IsEqual(_Race, "REALLY_HUMAN")
	THEN
		Set(_EffectDuration, INT:925)
	ELIF "c1"
		IsEqual(_Race, "REALLY_LIZARD")
	THEN
		Set(_EffectDuration, INT:500)
	ENDIF
	SetVar(_Character, "LeaderLib_Timer_CompleteEvent", STRING:"LLWEAPONEX_Greatbow_StopAttackFX")
	SetVar(_Character, "LeaderLib_Timer_Delay", _EffectDuration)
	CharacterEvent(_Character, "LeaderLib_Commands_StartObjectTimer")
	
	CharacterPlayLoopEffect(_FXHandle, _Character, _Effect, _Bone)
	SetVar(_Character, "LLWEAPONEX_Greatbow_AttackFXHandle", _FXHandle)
ENDIF

EVENT LLWEAPONEX_Greatbows_StopAttackFX
VARS
	CHARACTER:_Character
	INT64:_FXHandle
	STRING:_Effect
	FIXEDSTRING:_Bone
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_Greatbow_StopAttackFX")
ACTIONS
IF "c1&!c2"
	GetVar(_FXHandle, _Character, "LLWEAPONEX_Greatbow_AttackFXHandle")
	IsEqual(_FXHandle, null)
THEN
	StopLoopEffect(_FXHandle, 1)
	SetVar(_Character, "LLWEAPONEX_Greatbow_AttackFXHandle", INT64:null)
ENDIF

EVENT LLWEAPONEX_Greatbows_LightningStrike
VARS
	CHARACTER:_Character
	FLOAT:_LevelF
	INT:_Level
	FLOAT3:_Position
ON
	OnCharacterEvent(_Character, "LLWEAPONEX_Greatbow_ExplodeLightningStrikeAtPosition")
ACTIONS
IF "c1"
	CharacterGetStat(_LevelF, _Character, Level)
THEN
	Cast(_Level, _LevelF)
ENDIF
IF "c1&!c2"
	GetVar(_Position, _Character, "LLWEAPONEX_Greatbow_LightningStrikePosition")
	IsEqual(_Position, null)
THEN
	ExplodeAt(_Position, Projectile_LLWEAPONEX_Greatbow_LightningStrike, _Level, _Character)
	SetVar(_Character, "LLWEAPONEX_Greatbow_LightningStrikePosition", FLOAT3:null)
ENDIF

EVENT LLWEAPONEX_Greatbows_BarrageBonusDamage
VARS
	CHARACTER:_Source
	CHARACTER:_Target
	FLOAT:_Movement
	STRING:_Str
	FIXEDSTRING:_StrF
ON
	OnCharacterCharacterEvent(_Target, _Source, "LLWEAPONEX_Greatbows_FutureBarrage_CheckMovement")
ACTIONS
IF "c1|!c2|c3|c4&(c5|c6)"
	IsTagged(_Target, "LeaderLib_IsDisabled")
	CharacterCanMove(_Target)
	CharacterHasStatus(_Target, WEB)
	CharacterGetStat(_Movement, _Target, Movement)
	IsLessThen(_Movement, 0.01)
	IsEqual(_Movement, 0)
THEN
	//ExplodeAt(_Target, Projectile_LLWEAPONEX_Status_Greatbow_FutureBarrage_BonusDamage, -1, _Source)
	CharacterApplyStatus(_Target, LLWEAPONEX_GREATBOW_FUTUREBARRAGE_BONUSDAMAGE, 0, 1, _Source)
ENDIF
