Version 1
SubGoalCombiner SGC_AND
INITSECTION
KBSECTION
//REGION UPDATES
PROC
LeaderLib_Mods_OnVersionChanged(c60718c3-ba22-4702-9c5d-5ad92b41ba5f, (INTEGER)_LastVersion, (INTEGER)_NextVersion)
AND
_LastVersion < 152567808
THEN
SysClear("DB_LLWEAPONEX_Equipment_TrackedItems", 2);
SysClear("DB_LLWEAPONEX_Equipment_ActiveTags", 3);
//END_REGION

//REGION TRACK_UNARMED
PROC
LLWEAPONEX_Equipment_TrackUnarmed((CHARACTERGUID)_Char)
THEN
DB_LLWEAPONEX_Equipment_ActiveTags(_Char, "LLWEAPONEX_Unarmed", NULL_00000000-0000-0000-0000-000000000000);
LLWEAPONEX_WeaponMastery_TrackMastery(_Char, NULL_00000000-0000-0000-0000-000000000000, "LLWEAPONEX_Unarmed");
SetTag(_Char, "LLWEAPONEX_Unarmed");

PROC
LLWEAPONEX_Equipment_OnItemEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLWEAPONEX_Equipment_ActiveTags(_Char, "LLWEAPONEX_Unarmed", NULL_00000000-0000-0000-0000-000000000000)
THEN
NOT DB_LLWEAPONEX_Equipment_ActiveTags(_Char, "LLWEAPONEX_Unarmed", NULL_00000000-0000-0000-0000-000000000000);
NOT DB_LLWEAPONEX_WeaponMastery_Temp_ActiveMasteries(_Char, NULL_00000000-0000-0000-0000-000000000000, "LLWEAPONEX_Unarmed");
LLWEAPONEX_WeaponMastery_Internal_CheckRemovedMasteries(_Char, "LLWEAPONEX_Unarmed");

PROC
LLWEAPONEX_Equipment_OnItemEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item)
THEN
NRD_ModCall("WeaponExpansion", "OnItemEquipped", (STRING)_Char, (STRING)_Item);
//END_REGION

//REGION BOOST_SWAP_TIMER
PROC
LLWEAPONEX_Items_SaveGeneratedItem((ITEMGUID)_Item)
THEN
DB_LLWEAPONEX_Items_Temp_Generated(_Item);

IF
TimerFinished("Timers_LLWEAPONEX_SwapGeneratedItemBoosts")
AND
DB_LLWEAPONEX_Items_Temp_Generated(_Item)
AND
GetUUID(_Item, _UUID)
THEN
NOT DB_LLWEAPONEX_Items_Temp_Generated(_Item);
NRD_ModCall("WeaponExpansion", "SwapDeltaMods", _UUID);
//END_REGION

//REGION TRACK_EQUIPMENT
PROC
LLWEAPONEX_Equipment_TrackItem((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_WeaponType, (STRING)_EquippedTag, (INTEGER)_IsPlayer)
THEN
DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item, _WeaponType);
DB_LLWEAPONEX_Equipment_ActiveTags(_Char, _EquippedTag, _Item);
SetTag(_Char, _EquippedTag);

PROC
LLWEAPONEX_Equipment_ClearItem((CHARACTERGUID)_Char, (ITEMGUID)_Item, (INTEGER)_IsPlayer)
AND
DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item, _WeaponType)
THEN
NOT DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item, _WeaponType);
LLWEAPONEX_Equipment_OnTaggedItemUnEquipped(_Char, _Item, _WeaponType, _IsPlayer);

PROC
LLWEAPONEX_Equipment_ClearItem((CHARACTERGUID)_Char, (ITEMGUID)_Item, (INTEGER)_IsPlayer)
AND
DB_LLWEAPONEX_Equipment_ActiveTags(_Char, _Tag, _Item)
THEN
NOT DB_LLWEAPONEX_Equipment_ActiveTags(_Char, _Tag, _Item);
LLWEAPONEX_Equipment_Internal_CheckTags(_Char, _Tag);

PROC
LLWEAPONEX_Equipment_Internal_CheckTags((CHARACTERGUID)_Char, (STRING)_Tag)
AND
NOT DB_LLWEAPONEX_Equipment_ActiveTags(_Char, _Tag, _)
THEN
ClearTag(_Char, _Tag);
//END_REGION

//REGION TAG_EQUIPPED_EVENTS
PROC
LLWEAPONEX_Equipment_OnTaggedItemEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Tag, (INTEGER)_IsPlayer)
THEN
DB_NOOP(1);

PROC
LLWEAPONEX_Equipment_OnTaggedItemUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Tag, (INTEGER)_IsPlayer)
AND
DB_LLWEAPONEX_Temp_AnimationSetOverride(_Char, "LLWEAPONEX_Override1", _Tag)
THEN
LLWEAPONEX_AnimationSetOverride_Clear(_Char);
//END_REGION

//REGION TEMPLATE_EVENTS
PROC
LLWEAPONEX_OnItemTemplateEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
THEN
DB_NOOP(1);

PROC
LLWEAPONEX_OnItemTemplateUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
AND
GetUUID(_Char, _UUID)
THEN
NRD_ModCall("WeaponExpansion", "OnItemTemplateUnEquipped", _UUID, (STRING)_Item, _Template);
//END_REGION

//REGION CLEANUP
PROC
LLWEAPONEX_Equipment_Internal_CleanupEntry((CHARACTERGUID)_Char, (ITEMGUID)_Item, 1, 1)
AND
GetTemplate(_Item, _Template)
THEN
LLWEAPONEX_OnItemTemplateUnEquipped(_Char, _Item, _Template);

PROC
LLWEAPONEX_Equipment_Internal_CleanupEntry((CHARACTERGUID)_Char, (ITEMGUID)_Item, 1, (INTEGER)_ItemExists)
AND
LLWEAPONEX_Ext_QRY_IsPlayer(_Char, _IsPlayer)
THEN
LLWEAPONEX_Equipment_ClearItem(_Char, _Item, _IsPlayer);

PROC
LLWEAPONEX_Equipment_Internal_CleanupEntry((CHARACTERGUID)_Char, (ITEMGUID)_Item, 0, (INTEGER)_ItemExists)
THEN
LLWEAPONEX_Equipment_ClearItem(_Char, _Item, 1);

PROC
LLWEAPONEX_Equipment_Internal_ClearEntries((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item, _WeaponType)
THEN
NOT DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item, _WeaponType);

PROC
LLWEAPONEX_Equipment_Internal_ClearEntries((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLWEAPONEX_Equipment_ActiveTags(_Char, _Tag, _Item)
THEN
NOT DB_LLWEAPONEX_Equipment_ActiveTags(_Char, _Tag, _Item);

PROC
LLWEAPONEX_Equipment_Internal_ClearEntries((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLWEAPONEX_Temp_AnimationSetOverride(_Char, _Override, _Tag)
THEN
NOT DB_LLWEAPONEX_Temp_AnimationSetOverride(_Char, _Override, _Tag);
//END_REGION
EXITSECTION
ENDEXITSECTION
ParentTargetEdge "LaughingLeader_WeaponExpansion"