Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Skills_InitSettings();
KBSECTION
IF
DB_LeaderLib_Settings_Initialized(1)
THEN
LLWEAPONEX_Skills_InitSettings();

PROC
LLWEAPONEX_Skills_InitSettings()
AND
DB_LeaderLib_Settings_Initialized(1)
AND
NOT LeaderLib_Array_QRY_DataExists("LLWEAPONEX_ChaosSurfaces")
THEN
LeaderLib_Helper_BuildSurfaceList("LLWEAPONEX_ChaosSurfaces", "Standard", "Ground", "", 1);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:WeaponSkills:Settings] Initialized [LLWEAPONEX_ChaosSurfaces] surface list/array.");

IF
DB_LeaderLib_Array_Data("LLWEAPONEX_ChaosSurfaces", _Index, _Surface)
AND
IntegertoString(_Index, _IndexStr)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:WeaponSkills:Settings] Added surface [",_Surface,"] to array [LLWEAPONEX_ChaosSurfaces] at index [",_IndexStr,"]");

//REGION CHAOS_SKILLS
/*
IF
CharacterUsedSkillAtPosition(_Character, _tx, _ty, _tz, "Cone_LLWEAPONEX_DebugConeSkill", _, _)
AND
GetPosition(_Character, _x, _y, _z)
THEN
*/

IF
CharacterUsedSkill(_Character, "Projectile_LLWEAPONEX_ChaosSlash", _, _)
THEN
DB_LLWEAPONEX_Skills_Temp_ChaosSlashCaster(_Character);

IF
CharacterUsedSkill(_Character, "Projectile_LLWEAPONEX_EnemyChaosSlash", _, _)
THEN
DB_LLWEAPONEX_Skills_Temp_ChaosSlashCaster(_Character);

IF
StoryEvent((ITEMGUID)_MovingObject, "LLWEAPONEX_Commands_StartDrawingChaosSurface")
AND
DB_LLWEAPONEX_Skills_Temp_ChaosSlashCaster(_Character)
AND
GetVarFloat(_MovingObject, "LLWEAPONEX_Radius", _Radius)
AND
GetVarFloat(_MovingObject, "LLWEAPONEX_Lifetime", _Lifetime)
AND
LeaderLib_Array_QRY_Random("LLWEAPONEX_ChaosSurfaces")
AND
DB_LeaderLib_Array_Temp_RandomResult("LLWEAPONEX_ChaosSurfaces", _Index, _Surface)
AND
DrawSurfaceOnPath(_Character, _MovingObject, _Surface, _Radius, _Lifetime, _SurfaceActionHandle)
THEN
NOT DB_LeaderLib_Array_Temp_RandomResult("LLWEAPONEX_ChaosSurfaces", _Index, _Surface);
NOT DB_LLWEAPONEX_Skills_Temp_ChaosSlashCaster(_Character);
DB_LLWEAPONEX_Skills_Temp_ChaosSlicePath(_Character, _MovingObject, _SurfaceActionHandle, _Surface);

IF
CharacterItemEvent(_Character, _MovingObject, "LLWEAPONEX_Commands_StopDrawingChaosSurface")
AND
DB_LLWEAPONEX_Skills_Temp_ChaosSlicePath(_Character, _MovingObject, _SurfaceActionHandle, _Surface)
AND
GetPosition(_MovingObject, _x, _y, _z)
THEN
NOT DB_LLWEAPONEX_Skills_Temp_ChaosSlicePath(_Character, _MovingObject, _SurfaceActionHandle,_Surface);
StopDrawSurfaceOnPath(_SurfaceActionHandle);
ItemDestroy(_MovingObject);
CreateSurfaceAtPosition(_x, _y, _z, _Surface, 1.0, 12.0);

IF
CharacterUsedSkill(_Character, "Rush_LLWEAPONEX_EnemyChaosCharge", _, _)
AND
LeaderLib_Array_QRY_Random("LLWEAPONEX_ChaosSurfaces")
AND
DB_LeaderLib_Array_Temp_RandomResult("LLWEAPONEX_ChaosSurfaces", _Index, _Surface)
AND
DrawSurfaceOnPath(_Character, _Character, _Surface, 0.5, 12.0, _SurfaceActionHandle)
THEN
NOT DB_LeaderLib_Array_Temp_RandomResult("LLWEAPONEX_ChaosSurfaces", _Index, _Surface);
DB_LLWEAPONEX_Skills_Temp_ChaosChargeDrawing(_Character, _Surface, _SurfaceActionHandle);

IF
SkillCast(_Character, "Rush_LLWEAPONEX_EnemyChaosCharge", _, _)
THEN
ProcObjectTimer(_Character, "LLWEAPONEX_Timers_StopChaosChargeDrawing", 800);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "LLWEAPONEX_Timers_StopChaosChargeDrawing")
AND
DB_LLWEAPONEX_Skills_Temp_ChaosChargeDrawing(_Character, _Surface, _SurfaceActionHandle)
THEN
NOT DB_LLWEAPONEX_Skills_Temp_ChaosChargeDrawing(_Character, _Surface, _SurfaceActionHandle);
StopDrawSurfaceOnPath(_SurfaceActionHandle);
CreateSurface(_Character, _Surface, 1.0, 12.0);

IF
CharacterStartAttackObject((CHARACTERGUID)_Defender, _, _Attacker)
AND
CharacterGetEquippedWeapon(_Attacker, (ITEMGUID)_Weapon)
AND
IsTagged(_Weapon, "LLWEAPONEX_Katana", 1)
THEN
CharacterCharacterSetEvent(_Attacker, _Defender, "LLWEAPONEX_Katana_OnAttack");
//END_REGION

IF
CharacterUsedSkill(_Character, "Teleportation_LLWEAPONEX_InstantTransmission", _, _)
THEN
ObjectSetFlag(_Character, "LLWEAPONEX_BlockTeleportFalling", 0);

IF
SkillCast(_Character, "Teleportation_LLWEAPONEX_InstantTransmission", _, _)
THEN
ObjectClearFlag(_Character, "LLWEAPONEX_BlockTeleportFalling");

//REGION RALLY
IF
CharacterStatusRemoved(_Char, "LLWEAPONEX_BANNER_RALLY_DIVINEORDER", _)
THEN
LLWEAPONEX_Skills_OnRallyFinished(_Char);

IF
CharacterStatusRemoved(_Char, "LLWEAPONEX_BANNER_RALLY_DWARVES", _)
THEN
LLWEAPONEX_Skills_OnRallyFinished(_Char);

IF
ItemStatusRemoved(_Banner, "LLWEAPONEX_BANNER_RALLY_ITEM_LIFETIME", _)
AND
ItemIsDestroyed(_Banner, 0)
AND
ObjectExists(_Banner, 1)
AND
DB_LLWEAPONEX_Skills_Temp_RallyBanner(_Char, _Banner)
THEN
NOT DB_LLWEAPONEX_Skills_Temp_RallyBanner(_Char, _Banner);
PlayAnimation(_Banner, "destruction", "LLWEAPONEX_Banner_DestroyBanner");

PROC
LLWEAPONEX_Skills_OnRallyFinished((CHARACTERGUID)_Char)
AND
DB_LLWEAPONEX_Skills_Temp_RallyBanner(_Char, _Banner)
THEN
NOT DB_LLWEAPONEX_Skills_Temp_RallyBanner(_Char, _Banner);
PlayAnimation(_Banner, "destruction", "LLWEAPONEX_Banner_DestroyBanner");

IF
StoryEvent((ITEMGUID)_Banner, "LLWEAPONEX_Banner_DestroyBanner")
THEN
ItemRemove(_Banner);

IF
CharacterUsedSkillAtPosition(_Char, _x, _y, _z, "Dome_LLWEAPONEX_Banner_Rally_DivineOrder", _, _)
THEN
DB_LLWEAPONEX_Skills_Temp_RallyTarget(_Char, _x, _y, _z, "Dome_LLWEAPONEX_Banner_Rally_DivineOrder", "LLWEAPONEX_Skill_Rally_Banner_DivineOrder_A_d5c842bf-8eb0-46cd-a928-e4f989f91010");

IF
CharacterUsedSkillAtPosition(_Char, _x, _y, _z, "Dome_LLWEAPONEX_Banner_Rally_Dwarves", _, _)
THEN
DB_LLWEAPONEX_Skills_Temp_RallyTarget(_Char, _x, _y, _z, "Dome_LLWEAPONEX_Banner_Rally_Dwarves", "LLWEAPONEX_Skill_Rally_Banner_Dwarves_A_cfdf3976-4696-473d-8f4b-fd1461f8d708");

IF
SkillCast(_Char, "Dome_LLWEAPONEX_Banner_Rally_DivineOrder", _, _)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 100, "LLWEAPONEX_Timers_CreateRallyBanner", "LLWEAPONEX_Timers_CreateRallyBanner_DivineOrder");

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_Timers_CreateRallyBanner_DivineOrder")
THEN
LLWEAPONEX_Skills_Rally_CreateBanner(_Char, "Dome_LLWEAPONEX_Banner_Rally_DivineOrder");

IF
SkillCast(_Char, "Dome_LLWEAPONEX_Banner_Rally_Dwarves", _, _)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 100, "LLWEAPONEX_Timers_CreateRallyBanner", "LLWEAPONEX_Timers_CreateRallyBanner_Dwarves");

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_Timers_CreateRallyBanner_Dwarves")
THEN
LLWEAPONEX_Skills_Rally_CreateBanner(_Char, "Dome_LLWEAPONEX_Banner_Rally_Dwarves");

PROC
LLWEAPONEX_Skills_Rally_CreateBanner((CHARACTERGUID)_Char, (STRING)_Skill)
AND
DB_LLWEAPONEX_Skills_Temp_RallyTarget(_Char, _x, _y, _z, _Skill, _Template)
AND
RealSum(_x, 20.0, _tx)
AND
CreateItemTemplateAtPosition(_Template, _tx, _y, _z, _Banner)
THEN
NOT DB_LLWEAPONEX_Skills_Temp_RallyTarget(_Char, _x, _y, _z, _Skill, _Template);
DB_LLWEAPONEX_Skills_Temp_RallyBanner(_Char, _Banner);
ApplyStatus(_Banner, "LLWEAPONEX_BANNER_RALLY_ITEM_LIFETIME", 30.0, 1, _Char);
PlayAnimation(_Banner, "spawn");
TeleportToPosition(_Banner, _x, _y, _z, "", 0, 1);

PROC
LLWEAPONEX_Skills_Rally_CreateBanner((CHARACTERGUID)_Char, (STRING)_Skill)
AND
CharacterIsPlayer(_Char, _a)
AND
CharacterIsPartyMember(_Char, _b)
AND
IntegerMax(_a, _b, 1)
AND
CharacterGameMaster(_Char, 0)
AND
DB_IsPlayer(_PartyMember)
AND
_Char != _PartyMember
AND
CharacterIsInPartyWith(_Char, _PartyMember, 1)
THEN
CharacterAddSkill(_PartyMember, "Shout_LLWEAPONEX_Banner_Rally_Teleport", 1);
DB_LLWEAPONEX_Skills_Temp_RallyPartyMember(_Char, _PartyMember);

PROC
LLWEAPONEX_Skills_Rally_CreateBanner((CHARACTERGUID)_Char, (STRING)_Skill)
AND
LLWEAPONEX_QRY_CharacterIsNpcOrGM(_Char)
AND
DB_LeaderLib_GM_NpcParty_Active(_Char, _PartyMember)
THEN
CharacterAddSkill(_PartyMember, "Shout_LLWEAPONEX_Banner_Rally_Teleport", 1);
DB_LLWEAPONEX_Skills_Temp_RallyPartyMember(_Char, _PartyMember);

IF
SkillCast(_PartyMember, "Shout_LLWEAPONEX_Banner_Rally_Teleport", _, _)
AND
DB_LLWEAPONEX_Skills_Temp_RallyPartyMember(_Char, _PartyMember)
AND
DB_LLWEAPONEX_Skills_Temp_RallyBanner(_Char, _Banner)
THEN
NOT DB_LLWEAPONEX_Skills_Temp_RallyPartyMember(_Char, _PartyMember);
CharacterRemoveSkill(_PartyMember, "Shout_LLWEAPONEX_Banner_Rally_Teleport");
TeleportTo(_PartyMember, _Banner, "", 1, 0);
CharacterCharacterSetEvent(_Char, _PartyMember, "LLWEAPONEX_Rally_PartyMemberTeleported");

PROC
LLWEAPONEX_Skills_OnRallyFinished((CHARACTERGUID)_Char)
AND
DB_LLWEAPONEX_Skills_Temp_RallyPartyMember(_Char, _PartyMember)
THEN
NOT DB_LLWEAPONEX_Skills_Temp_RallyPartyMember(_Char, _PartyMember);
CharacterRemoveSkill(_PartyMember, "Shout_LLWEAPONEX_Banner_Rally_Teleport");

//Final member teleported.
IF
CharacterCharacterEvent(_Char, _PartyMember, "LLWEAPONEX_Rally_PartyMemberTeleported")
AND
NOT DB_LLWEAPONEX_Skills_Temp_RallyPartyMember(_Char, _)
AND
HasActiveStatus(_Char, "LLWEAPONEX_BANNER_RALLY_DIVINEORDER", 1)
THEN
RemoveStatus(_Char, "LLWEAPONEX_BANNER_RALLY_DIVINEORDER");

IF
CharacterCharacterEvent(_Char, _PartyMember, "LLWEAPONEX_Rally_PartyMemberTeleported")
AND
NOT DB_LLWEAPONEX_Skills_Temp_RallyPartyMember(_Char, _)
AND
HasActiveStatus(_Char, "LLWEAPONEX_BANNER_RALLY_DWARVES", 1)
THEN
RemoveStatus(_Char, "LLWEAPONEX_BANNER_RALLY_DWARVES");
//END_REGION


EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"
