Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Common", "S_LLWEAPONEX_CombatShield_Blackring_A_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Uncommon", "S_LLWEAPONEX_CombatShield_Blackring_A_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Rare", "S_LLWEAPONEX_CombatShield_Blackring_A_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Epic", "S_LLWEAPONEX_CombatShield_Blackring_A_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Legendary", "S_LLWEAPONEX_CombatShield_Blackring_A_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Blackring_A", "Divine", "S_LLWEAPONEX_CombatShield_Blackring_A_Divine");

DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Common", "S_LLWEAPONEX_CombatShield_Common_A_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Uncommon", "S_LLWEAPONEX_CombatShield_Common_A_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Rare", "S_LLWEAPONEX_CombatShield_Common_A_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Epic", "S_LLWEAPONEX_CombatShield_Common_A_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Legendary", "S_LLWEAPONEX_CombatShield_Common_A_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Common_A", "Divine", "S_LLWEAPONEX_CombatShield_Common_A_Divine");

DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Common", "S_LLWEAPONEX_CombatShield_Dwarves_A_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Uncommon", "S_LLWEAPONEX_CombatShield_Dwarves_A_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Rare", "S_LLWEAPONEX_CombatShield_Dwarves_A_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Epic", "S_LLWEAPONEX_CombatShield_Dwarves_A_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Legendary", "S_LLWEAPONEX_CombatShield_Dwarves_A_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Dwarves_A", "Divine", "S_LLWEAPONEX_CombatShield_Dwarves_A_Divine");

DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Common", "S_LLWEAPONEX_CombatShield_Elves_B_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Uncommon", "S_LLWEAPONEX_CombatShield_Elves_B_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Rare", "S_LLWEAPONEX_CombatShield_Elves_B_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Epic", "S_LLWEAPONEX_CombatShield_Elves_B_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Legendary", "S_LLWEAPONEX_CombatShield_Elves_B_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Elves_B", "Divine", "S_LLWEAPONEX_CombatShield_Elves_B_Divine");

DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Common", "S_LLWEAPONEX_CombatShield_Humans_A_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Uncommon", "S_LLWEAPONEX_CombatShield_Humans_A_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Rare", "S_LLWEAPONEX_CombatShield_Humans_A_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Epic", "S_LLWEAPONEX_CombatShield_Humans_A_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Legendary", "S_LLWEAPONEX_CombatShield_Humans_A_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Humans_A", "Divine", "S_LLWEAPONEX_CombatShield_Humans_A_Divine");

DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Common", "S_LLWEAPONEX_CombatShield_Lizards_C_Common");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Uncommon", "S_LLWEAPONEX_CombatShield_Lizards_C_Uncommon");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Rare", "S_LLWEAPONEX_CombatShield_Lizards_C_Rare");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Epic", "S_LLWEAPONEX_CombatShield_Lizards_C_Epic");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Legendary", "S_LLWEAPONEX_CombatShield_Lizards_C_Legendary");
DB_LLWEAPONEX_DualShields_CombatShieldGenerator("WPN_LLWEAPONEX_DualShields_Lizards_C", "Divine", "S_LLWEAPONEX_CombatShield_Lizards_C_Divine");

DB_LLWEAPONEX_DualShields_Templates("WPN_LLWEAPONEX_CombatShield_Blackring_1H_A_ec353f1e-c1ca-46d1-83ef-e9f4fea14475", "WPN_LLWEAPONEX_Shield_DualShields_Blackring_A_9d98560d-916a-4ea0-8064-8a5ac8c13b45");
DB_LLWEAPONEX_DualShields_Templates("WPN_LLWEAPONEX_CombatShield_Common_1H_A_8c7da07b-ad11-4a0c-8406-0261977042b6", "WPN_LLWEAPONEX_Shield_DualShields_Common_A_8df9c20b-98f5-4f48-8438-573456fdfbf1");
DB_LLWEAPONEX_DualShields_Templates("WPN_LLWEAPONEX_CombatShield_Dwarves_1H_A_bc034226-19bd-45e6-be7d-ec0d28c2e412", "WPN_LLWEAPONEX_Shield_DualShields_Dwarves_A_91fd35cb-93dd-4489-9cf5-2dcc9b0ac168");
DB_LLWEAPONEX_DualShields_Templates("WPN_LLWEAPONEX_CombatShield_Elves_1H_B_1268f5f0-e484-42ea-8c13-0014e6aeaaad", "WPN_LLWEAPONEX_Shield_DualShields_Elves_B_565e917f-ed53-40ed-8093-2133bc3e2a50");
DB_LLWEAPONEX_DualShields_Templates("WPN_LLWEAPONEX_CombatShield_Humans_1H_A_3a404dab-4862-4490-aa0f-bc27d06fdc6c", "WPN_LLWEAPONEX_Shield_DualShields_Humans_A_c0bf83ff-5a55-4ac8-8a2b-9d9044c10d10");
DB_LLWEAPONEX_DualShields_Templates("WPN_LLWEAPONEX_CombatShield_Lizards_1H_C_067f48be-857d-43a8-bd0a-add59f025843", "WPN_LLWEAPONEX_Shield_DualShields_Lizards_C_4f6f404a-06df-4041-bda6-87b7e24fd52e");

KBSECTION
QRY
LLWEAPONEX_DualShields_QRY_ItemLevelSet((ITEMGUID)_Item)
AND
GetVarInteger(_Item, "LeaderLib_Level", _Level)
AND
_Level > 0
THEN
LeaderLog_LogInt("DEBUG", "[LLWEAPONEX:DualShields:QRY:ItemLevelSet] Dual Shields level variable is [", _Level, "].");

QRY
LLWEAPONEX_DualShields_QRY_HasCombatShield((ITEMGUID)_Item)
AND
GetVarObject(_Item, "LLWEAPONEX_CombatShield", (ITEMGUID)_CombatShield)
AND
ObjectExists(_CombatShield, 1)
THEN
DB_NOOP(1);

//REGION UPDATING
/*
//Dual Shields eating items fix
PROC
LeaderLib_Mods_OnVersionChanged(c60718c3-ba22-4702-9c5d-5ad92b41ba5f, (INTEGER)_LastVersion, (INTEGER)_NextVersion)
AND
DB_IsPlayer(_Char)
THEN
InventoryLaunchTagIterator(_Char, "LLWEAPONEX_DualShields", "", "LLWEAPONEX_Iterators_FoundDualShield_RemoveItems", "");

//Combat shield identify fix
PROC
LeaderLib_Mods_OnVersionChanged(c60718c3-ba22-4702-9c5d-5ad92b41ba5f, (INTEGER)_LastVersion, (INTEGER)_NextVersion)
AND
DB_IsPlayer(_Char)
THEN
InventoryLaunchTagIterator(_Char, "LLWEAPONEX_DualShields", "", "LLWEAPONEX_Iterators_FoundDualShield_Identify", "");
*/

IF
StoryEvent((ITEMGUID)_Shield, "LLWEAPONEX_Iterators_FoundDualShield_Identify")
THEN
ContainerIdentifyAll(_Shield);

IF
StoryEvent((ITEMGUID)_Shield, "LLWEAPONEX_Iterators_FoundDualShield_RemoveItems")
AND
GetInventoryOwner(_Shield, _Owner)
THEN
DB_LLWEAPONEX_DualShields_Temp_RemovalIterator(_Shield, _Owner);
InventoryLaunchIterator(_Shield, "LLWEAPONEX_Iterators_DualShields_RemoveItem", "");
LeaderLib_Timers_StartObjectTimer(_Shield, 250, "Timers_LLWEAPONEX_DualShields_RemoveItemIteratorDone", "LLWEAPONEX_DualShields_RemoveItemIteratorDone");

IF
StoryEvent((ITEMGUID)_Shield, "LLWEAPONEX_DualShields_RemoveItemIteratorDone")
AND
DB_LLWEAPONEX_DualShields_Temp_RemovalIterator(_Shield, _Owner)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_RemovalIterator(_Shield, _Owner);

IF
StoryEvent((ITEMGUID)_Item, "LLWEAPONEX_Iterators_DualShields_RemoveItem")
AND
GetInventoryOwner(_Item, (ITEMGUID)_Shield)
AND
ItemGetAmount(_Item, _Amount)
AND
DB_LLWEAPONEX_DualShields_Temp_RemovalIterator(_Shield, _Owner)
THEN
ItemToInventory(_Item, _Owner, _Amount, 1, 0);
//END_REGION

//REGION UNIQUE_SHIELDS
PROC
LLWEAPONEX_OnItemTemplateEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "WPN_UNIQUE_LLWEAPONEX_Shield_DualShields_GiantDoor_A_5e8ca310-616d-4f18-b455-e510dade5b62")
AND
ObjectGetFlag(_Item, "LLWEAPONEX_DualShields_GeneratedCombatShield", 0)
AND
GetPosition(_Char, _x, _y, _z)
AND
CreateItemTemplateAtPosition("WPN_UNIQUE_LLWEAPONEX_CombatShield_GiantDoor_A_b4748a58-427c-4652-bf6c-d596ddcc134d", _x, _y, _z, _CombatShield)
AND
CharacterGetLevel(_Char, _Level)
THEN
ObjectSetFlag(_Item, "LLWEAPONEX_DualShields_GeneratedCombatShield", 0);
SetTag(_CombatShield, "LLWEAPONEX_Debug_StorableCombatShield");
SetVarObject(_Item, "LLWEAPONEX_CombatShield", _CombatShield);

SetVarObject(_CombatShield, "LLWEAPONEX_ParentDualShield", _Item);
SetVarFixedString(_CombatShield, "LeaderLib_Rarity", "Unique");
ItemSetOwner(_CombatShield, _Char);

ItemLevelUpTo(_Item, _Level);
ItemLevelUpTo(_CombatShield, _Level);
SetVarInteger(_Item, "LeaderLib_Level", _Level);
SetVarInteger(_CombatShield, "LeaderLib_Level", _Level);

ItemToInventory(_CombatShield, _Item, 1, 0, 0);
//END_REGION

//REGION GENERATE_COMBAT_SHIELD
PROC
LLWEAPONEX_DualShieldS_Internal_GenerateCombatShield((ITEMGUID)_Shield)
AND
NOT LLWEAPONEX_DualShields_QRY_HasCombatShield(_Shield)
AND
GetVarInteger(_Shield, "LeaderLib_Level", _Level)
AND
GetVarFixedString(_Shield, "LeaderLib_Rarity", _Rarity)
AND
GetStatString(_Shield, _Stat)
AND
DB_LLWEAPONEX_DualShields_CombatShieldGenerator(_Stat, _Rarity, _TreasureTable)
THEN
DB_LLWEAPONEX_DualShields_Temp_ListenForCombatShield(_Shield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_DualShields_RaritySet] [",_Stat,"] Generating combat shield with rarity [",_Rarity,"] from treasure table [",_TreasureTable,"].");
GenerateTreasure(_Shield, _TreasureTable, _Level, NULL_00000000-0000-0000-0000-000000000000);

PROC
LLWEAPONEX_DualShields_CombatShield_StartCloning((ITEMGUID)_Shield, (ITEMGUID)_CombatShield)
THEN
DB_NOOP(1);

PROC
LLWEAPONEX_DualShields_CombatShield_OnCloned((ITEMGUID)_Shield, (CHARACTERGUID)_Owner, (STRING)_Rarity, (INTEGER)_Level)
THEN
DB_NOOP(1);

IF
ItemAddedToContainer(_CombatShield, _Shield)
AND
DB_LLWEAPONEX_DualShields_Temp_ListenForCombatShield(_Shield)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ListenForCombatShield(_Shield);
LLWEAPONEX_DualShields_CombatShield_StartCloning(_Shield, _CombatShield);

PROC
LLWEAPONEX_DualShields_CombatShield_StartCloning((ITEMGUID)_Shield, (ITEMGUID)_CombatShield)
AND
GetVarFixedString(_Shield, "LeaderLib_Rarity", _Rarity)
AND
GetVarInteger(_Shield, "LeaderLib_Level", _Level)
AND
ItemGetOwner(_Shield, _Owner)
THEN
NRD_ItemCloneBegin(_CombatShield);
NRD_ItemCloneSetString("ItemType", _Rarity);
NRD_ItemCloneSetString("GenerationItemType", _Rarity);
NRD_ItemCloneSetInt("GenerationLevel", _Level);
LLWEAPONEX_DualShields_CombatShield_OnCloned(_Shield, _Owner, _Rarity, _Level);
ItemRemove(_CombatShield);

PROC
LLWEAPONEX_DualShields_CombatShield_OnCloned((ITEMGUID)_Shield, (CHARACTERGUID)_Owner, (STRING)_Rarity, (INTEGER)_Level)
AND
NRD_ItemClone((ITEMGUID)_CombatShield)
THEN
ItemToInventory(_CombatShield, _Shield, 1, 0, 0);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:ItemAddedToContainer] Combat shield generated.");
ObjectSetFlag(_Shield, "LLWEAPONEX_DualShields_GeneratedCombatShield", 0);
SetTag(_CombatShield, "LLWEAPONEX_Debug_StorableCombatShield");
SetVarObject(_Shield, "LLWEAPONEX_CombatShield", _CombatShield);
SetVarObject(_CombatShield, "LLWEAPONEX_ParentDualShield", _Shield);
SetVarFixedString(_CombatShield, "LeaderLib_Rarity", _Rarity);
SetVarInteger(_CombatShield, "LeaderLib_Level", _Level);
ItemSetOwner(_CombatShield, _Owner);
LeaderLib_Deltamods_ApplyDeltamodsByGroup(_CombatShield, "WeaponExpansion.CombatShields");
//ItemSetOnlyOwnerCanUse(_CombatShield, 1);
LLWEAPONEX_DualShields_CombatShieldGenerated(_Owner, _Shield, _CombatShield);
//END_REGION

//REGION EQUIPPED
PROC
LLWEAPONEX_OnItemTemplateEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
AND
IsTagged(_Item, "LLWEAPONEX_DualShields", 1)
AND
ObjectGetFlag(_Item, "LLWEAPONEX_DualShields_GeneratedCombatShield", _GeneratedShield)
THEN
LLWEAPONEX_DualShields_OnDualShieldEquipped(_Char, _Item, _GeneratedShield);

PROC
LLWEAPONEX_DualShields_OnDualShieldEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Shield, 1)
AND
LeaderLib_Variables_QRY_ObjectVariableSet(_Shield, "LLWEAPONEX_CombatShield")
AND
GetVarObject(_Shield, "LLWEAPONEX_CombatShield", (ITEMGUID)_CombatShield)
THEN
LLWEAPONEX_DualShields_EquipCombatShield(_Char, _Shield, _CombatShield);

PROC
LLWEAPONEX_DualShields_OnDualShieldEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Shield, 1)
AND
NOT LeaderLib_Variables_QRY_ObjectVariableSet(_Shield, "LLWEAPONEX_CombatShield")
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:OnDualShieldsEquipped] [ERROR] Item for variable [LLWEAPONEX_CombatShield] does not exist. Checking inventory.");
LLWEAPONEX_DualShields_FindCombatShield(_Char, _Shield, 1);

PROC
LLWEAPONEX_DualShields_OnDualShieldEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Shield, 0)
AND
LLWEAPONEX_DualShields_QRY_ItemLevelSet(_Shield)
THEN
LLWEAPONEX_DualShieldS_Internal_GenerateCombatShield(_Shield);

PROC
LLWEAPONEX_DualShields_OnDualShieldEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Shield, 0)
AND
NOT LLWEAPONEX_DualShields_QRY_ItemLevelSet(_Shield)
THEN
ItemSetOwner(_Shield, _Char);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_DualShields_OnDualShieldEquipped] Level/Rarity variables not set on Dual Shield. Running [LeaderLib_Commands_SetItemVariables].");
SetVarString(_Shield, "LeaderLib_ItemVariablesSetEvent", "LLWEAPONEX_DualShields_VariablesSet");
SetStoryEvent(_Shield, "LeaderLib_Commands_SetItemVariables");

IF
StoryEvent((ITEMGUID)_Shield, "LLWEAPONEX_DualShields_VariablesSet")
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_DualShields_VariablesSet] Generating combat shield.");
ObjectSetFlag(_Shield, "LLWEAPONEX_DualShields_EquipAfterGenerating", 0);
LLWEAPONEX_DualShieldS_Internal_GenerateCombatShield(_Shield);

PROC
LLWEAPONEX_DualShields_CombatShieldGenerated((CHARACTERGUID)_Owner, (ITEMGUID)_Shield, (ITEMGUID)_CombatShield)
AND
ObjectGetFlag(_Shield, "LLWEAPONEX_DualShields_EquipAfterGenerating", 1)
THEN
ObjectClearFlag(_Shield, "LLWEAPONEX_DualShields_EquipAfterGenerating");
ContainerIdentifyAll(_Shield);
LLWEAPONEX_DualShields_EquipCombatShield(_Owner, _Shield, _CombatShield);

PROC
LLWEAPONEX_DualShields_EquipCombatShield((CHARACTERGUID)_Char, (ITEMGUID)_Shield, (ITEMGUID)_CombatShield)
AND
NOT CharacterGetEquippedWeapon(_Char, _CombatShield)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:OnDualShieldsEquipped] Equipping combat shield stored in a dual shield.");
//DB_LLWEAPONEX_DualShields_Temp_ListenForEquipping(_Char, _Shield, _CombatShield);
ItemToInventory(_CombatShield, _Char, 1, 0, 0);
//LeaderLib_Helper_EquipInSlot(_Char, _CombatShield, "Weapon");
NRD_CharacterEquipItem(_Char, _CombatShield, "Weapon", 0, 0, 1, 1);
//CharacterUseItem(_Char, _CombatShield, "LLWEAPONEX_DualShields_CombatShieldEquipped");

/*
IF
//ItemEquipped(_CombatShield, _Char)
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_DualShields_CombatShieldEquipped")
AND
DB_LLWEAPONEX_DualShields_Temp_ListenForEquipping(_Char, _Shield, _CombatShield)
AND
CharacterGetEquippedItem(_Char, "Weapon", _CombatShield)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ListenForEquipping(_Char, _Shield, _CombatShield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_DualShields_CombatShieldEquipped] Combat shield was equipped.");

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_DualShields_CombatShieldEquipped")
AND
DB_LLWEAPONEX_DualShields_Temp_ListenForEquipping(_Char, _Shield, _CombatShield)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ListenForEquipping(_Char, _Shield, _CombatShield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_DualShields_CombatShieldEquipped] Combat shield was not equipped.");
ItemToInventory(_CombatShield, _Shield, 1, 0, 0);
ContainerIdentifyAll(_Shield);
CharacterUnequipItem(_Char, _Shield);

IF
CharacterUsedItemFailed(_Char, _CombatShield)
AND
DB_LLWEAPONEX_DualShields_Temp_ListenForEquipping(_Char, _Shield, _CombatShield)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ListenForEquipping(_Char, _Shield, _CombatShield);
ItemToInventory(_CombatShield, _Shield, 1, 0, 0);
CharacterUnequipItem(_Char, _Shield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:CharacterUsedItemFailed] Failed to equip combat shield. Generating a new shield.");
//ItemDestroy(_CombatShield);
//LLWEAPONEX_DualShields_OnDualShieldEquipped(_Char, _Shield, 0);
*/
//END_REGION

//REGION FIND_COMBAT_SHIELD
PROC
LLWEAPONEX_DualShields_FindCombatShield((CHARACTERGUID)_Char, (ITEMGUID)_Shield, 1)
THEN
DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Char, _Shield);
LeaderLib_Timers_StartCharacterItemTimer(_Char, _Shield, 250, "Timers_LLWEAPONEX_DualShields_FindCombatShield_Failed", "LLWEAPONEX_DualShields_FindCombatShield_Failed");
InventoryLaunchTagIterator(_Shield, "LLWEAPONEX_CombatShield", "", "LLWEAPONEX_Iterators_FoundCombatShield_Equip", "");

IF
StoryEvent((ITEMGUID)_CombatShield, "LLWEAPONEX_Iterators_FoundCombatShield_Equip")
AND
GetInventoryOwner(_CombatShield, (ITEMGUID)_Shield)
AND
DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Char, _Shield)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Char, _Shield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_Iterators_FoundDualShield] Found combat shield in dual shield inventory. Setting variable & equipping.");
SetVarObject(_Shield, "LLWEAPONEX_CombatShield", _CombatShield);
SetVarObject(_CombatShield, "LLWEAPONEX_ParentDualShield", _Shield);
LLWEAPONEX_DualShields_EquipCombatShield(_Char, _Shield, _CombatShield);
LeaderLib_Timers_CancelCharacterItemTimer(_Char, _Shield, "Timers_LLWEAPONEX_DualShields_FindCombatShield_Failed");
LeaderLib_Timers_CancelCharacterItemTimer(_Char, _CombatShield, "Timers_LLWEAPONEX_DualShields_FindDualShield_Failed");

PROC
LLWEAPONEX_DualShields_FindCombatShield((CHARACTERGUID)_Char, (ITEMGUID)_Shield, 0)
THEN
DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Char, _Shield);
LeaderLib_Timers_StartCharacterItemTimer(_Char, _Shield, 250, "Timers_LLWEAPONEX_DualShields_FindCombatShield_Failed", "LLWEAPONEX_DualShields_FindCombatShield_Failed");
InventoryLaunchTagIterator(_Shield, "LLWEAPONEX_CombatShield", "", "LLWEAPONEX_Iterators_FoundCombatShield", "");

IF
StoryEvent((ITEMGUID)_CombatShield, "LLWEAPONEX_Iterators_FoundCombatShield")
AND
GetInventoryOwner(_CombatShield, (ITEMGUID)_Shield)
AND
DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Char, _Shield)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Char, _Shield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_Iterators_FoundDualShield] Found combat shield in dual shield inventory. Setting variable.");
SetVarObject(_Shield, "LLWEAPONEX_CombatShield", _CombatShield);
LeaderLib_Timers_CancelCharacterItemTimer(_Char, _Shield, "Timers_LLWEAPONEX_DualShields_FindCombatShield_Failed");
LeaderLib_Timers_CancelCharacterItemTimer(_Char, _CombatShield, "Timers_LLWEAPONEX_DualShields_FindDualShield_Failed");

IF
CharacterItemEvent(_Char, _Shield, "LLWEAPONEX_DualShields_FindCombatShield_Failed")
AND
DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Char, _Shield)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_ShieldIterator(_Char, _Shield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX_DualShields_FindCombatShield_Failed] Combat shield not found. Regenerating.");
LLWEAPONEX_DualShieldS_Internal_GenerateCombatShield(_Shield);
//END_REGION

//REGION UNEQUIPPED
PROC
LLWEAPONEX_OnItemTemplateUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
AND
IsTagged(_Item, "LLWEAPONEX_DualShields", 1)
AND
NOT DB_LLWEAPONEX_DualShields_Temp_Handled(_Char, _, _)
AND
CharacterGetEquippedItem(_Char, "Weapon", (ITEMGUID)_Mainhand)
AND
GetVarObject(_Item, "LLWEAPONEX_CombatShield", (ITEMGUID)_CombatShield)
AND
_CombatShield == _Mainhand
THEN
DB_LLWEAPONEX_DualShields_Temp_Handled(_Char, _Item, _Mainhand);
LeaderLib_Timers_StartObjectTimer(_Char, 500, "LLWEAPONEX_Timers_ResetDualShieldHandled", "LLWEAPONEX_DualShields_ResetHandled");
ItemToInventory(_Mainhand, _Item, 1, 0, 0);

PROC
LLWEAPONEX_OnItemTemplateUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
AND
IsTagged(_Item, "LLWEAPONEX_CombatShield", 1)
AND
NOT DB_LLWEAPONEX_DualShields_Temp_Handled(_Char, _, _)
AND
GetVarObject(_Item, "LLWEAPONEX_ParentDualShield", (ITEMGUID)_Shield)
THEN 
DB_LLWEAPONEX_DualShields_Temp_Handled(_Char, _Shield, _Item);
LeaderLib_Timers_StartObjectTimer(_Char, 500, "LLWEAPONEX_Timers_ResetDualShieldHandled", "LLWEAPONEX_DualShields_ResetHandled");
ItemToInventory(_Item, _Shield, 1, 0, 0);
LLWEAPONEX_DualShields_Internal_UnequipParentShield(_Char, _Item, _Shield);

PROC
LLWEAPONEX_DualShields_Internal_UnequipParentShield((CHARACTERGUID)_Char, (ITEMGUID)_CombatShield, (ITEMGUID)_ParentShield)
AND
CharacterGetEquippedShield(_Char, _ParentShield)
THEN
CharacterUnequipItem(_Char, _ParentShield);

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_DualShields_ResetHandled")
AND
DB_LLWEAPONEX_DualShields_Temp_Handled(_Char, _Shield, _CombatShield)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_Handled(_Char, _Shield, _CombatShield);
//END_REGION

//REGION LEADERLIB_PRESET_APPLIED
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "LeaderLib_Timers_PresetMenu_OnPresetApplied")
AND
CharacterGetEquippedItem(_Char, "Shield", (ITEMGUID)_Shield)
AND
IsTagged(_Shield, "LLWEAPONEX_DualShields", 1)
AND
ObjectGetFlag(_Shield, "LLWEAPONEX_DualShields_GeneratedCombatShield", _GeneratedShield)
THEN
LLWEAPONEX_DualShields_OnDualShieldEquipped(_Char, _Shield, _GeneratedShield);
//END_REGION

//REGION CC_PRESET_FIX
/*
//Post-CC Preset Fix
PROC
LLWEAPONEX_OnCharacterCreationFinished((CHARACTERGUID)_Char)
AND
CharacterGetEquippedItem(_Char, "Shield", (ITEMGUID)_Shield)
AND
_Shield != NULL_00000000-0000-0000-0000-000000000000
AND
IsTagged(_Shield, "LLWEAPONEX_DualShields", 1)
AND
ObjectGetFlag(_Shield, "LLWEAPONEX_DualShields_GeneratedCombatShield", _GeneratedShield)
THEN
LLWEAPONEX_DualShields_OnDualShieldEquipped(_Char, _Shield, _GeneratedShield);
*/
//END_REGION

//REGION HUNKER_DOWN
IF
CharacterStatusApplied(_Char, "LLWEAPONEX_DUALSHIELDS_HUNKER_DOWN", _)
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Char, "LLWEAPONEX_DualShields_HunkeringDown", "WeaponExpansion");

IF
CharacterStatusApplied(_Char, "LLWEAPONEX_DUALSHIELDS_HUNKER_DOWN", _)
AND
IsTagged(_Char, "AI_PREFERRED_TARGET", 0)
THEN
SetTag(_Char, "AI_PREFERRED_TARGET");
DB_LLWEAPONEX_DualShields_Temp_AddedAITag(_Char);

IF
CharacterStatusRemoved(_Char, "LLWEAPONEX_DUALSHIELDS_HUNKER_DOWN", _)
AND
DB_LLWEAPONEX_DualShields_Temp_AddedAITag(_Char)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_AddedAITag(_Char);
ClearTag(_Char, "AI_PREFERRED_TARGET");

IF
CharacterStatusRemoved(_Char, "LLWEAPONEX_DUALSHIELDS_HUNKER_DOWN", _)
THEN
LeaderLib_ToggleScripts_DisableScriptForObjectAfterDelay(_Char, "LLWEAPONEX_DualShields_HunkeringDown", "WeaponExpansion", 500);
SetStoryEvent(_Char, "LLWEAPONEX_DualShields_RemoveHunkerDownAnimationOverride");
//END_REGION

//REGION SHIELD_COVER
IF
CharacterStatusRemoved(_Char, "LLWEAPONEX_COVERED", _)
AND
GetUUID(_Char, _UUID)
THEN
NRD_ModCall("WeaponExpansion", "DualShields_Cover_OnRemoved", _UUID);

IF
CharacterStatusApplied(_Char, "LLWEAPONEX_COVERED", (CHARACTERGUID)_Blocker)
AND GetUUID(_Char, _UUID)
AND GetUUID(_Blocker, _BlockerUUID)
THEN
NRD_ModCall("WeaponExpansion", "DualShields_Cover_OnApplied", _UUID, _BlockerUUID);
//END_REGION

//REGION IRON_MAIDEN
IF
CharacterStatusRemoved(_Char, "LLWEAPONEX_SHIELD_PRISON", _)
AND
HasActiveStatus(_Char, "LLWEAPONEX_SHIELD_PRISON_FX", 1)
THEN
ProcObjectTimerCancel(_Char, "LLWEAPONEX_Timers_RemoveShieldPrisonFX");
ProcObjectTimer(_Char, "LLWEAPONEX_Timers_RemoveShieldPrisonFX", 250);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "LLWEAPONEX_Timers_RemoveShieldPrisonFX")
AND
HasActiveStatus(_Char, "LLWEAPONEX_SHIELD_PRISON_FX", 1)
THEN
RemoveStatus(_Char, "LLWEAPONEX_SHIELD_PRISON_FX");
LeaderLib_Timers_StartObjectTimer(_Char, 250, "LLWEAPONEX_Timers_PlayShieldPrisonEndFX", "LLWEAPONEX_PlayShieldPrisonEndFX");

IF
StoryEvent(_Char, "LLWEAPONEX_PlayShieldPrisonEndFX")
AND
HasActiveStatus(_Char, "LLWEAPONEX_IRONMAIDEN_SHIELDPRISON_FX", 0)
THEN
PlayEffect(_Char, "LLWEAPONEX_FX_Status_ShieldPrison_End_01", "Dummy_Root");

IF
CharacterUsedSkillOnTarget(_Char, (CHARACTERGUID)_Target, "Target_LLWEAPONEX_IronMaiden", _, _)
AND
HasActiveStatus(_Target, "LLWEAPONEX_SHIELD_PRISON", 1)
THEN
RemoveStatus(_Target, "LLWEAPONEX_SHIELD_PRISON");
ApplyStatus(_Target, "LLWEAPONEX_IRONMAIDEN_SHIELDPRISON_FX", 3.0, 1, _Char);

IF
CharacterPrecogDying(_Char)
AND
IsTagged(_Char, "LLWEAPONEX_IRONMAIDEN_BONUS", 1)
THEN
ClearTag(_Char, "LLWEAPONEX_IRONMAIDEN_BONUS");
//END_REGION

//REGION RELOAD_STATS_FIND_COMBAT_SHIELD
/*
PROC
LLWEAPONEX_OnItemTemplateEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
AND
LeaderLog_QRY_Log("DEBUG", "[LLWEAPONEX_10_DualShields:OnItemEquipped] Item equipped: ", _Template)
AND
GetVarObject(_Item, "LLWEAPONEX_ParentDualShield", _Shield)
AND
String(_Shield, _ShieldStr)
AND
LeaderLog_QRY_Log("DEBUG", "[LLWEAPONEX_10_DualShields:OnItemEquipped] Parent dual shield: ", _ShieldStr)
THEN
DB_NOOP(1);
*/

PROC
LLWEAPONEX_OnItemTemplateEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
AND
IsTagged(_Item, "LLWEAPONEX_CombatShield", 1)
THEN
LLWEAPONEX_DualShields_Internal_OnCombatShieldEquipped(_Char, _Item, _Template);

PROC
LLWEAPONEX_DualShields_Internal_OnCombatShieldEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
AND
NOT CharacterGetEquippedShield(_Char, _)
AND
LeaderLib_Variables_QRY_ObjectVariableSet(_Item, "LLWEAPONEX_ParentDualShield")
AND
GetVarObject(_Item, "LLWEAPONEX_ParentDualShield", (ITEMGUID)_Shield)
AND
ObjectExists(_Shield, 1)
THEN
DB_LLWEAPONEX_DualShields_Temp_OnCombatShieldEquipped(_Char, _Item, _Shield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX_10_DualShields:OnItemEquipped] Equipping shield for combat shield.");
LeaderLib_Timers_StartCharacterItemTimer(_Char, _Shield, 250, "Timers_LeaderLib_Commands_EquipItem", "LeaderLib_Commands_EquipItem");

PROC
LLWEAPONEX_DualShields_Internal_OnCombatShieldEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Template)
AND
NOT DB_LLWEAPONEX_DualShields_Temp_OnCombatShieldEquipped(_Char, _Item, _)
AND
NOT LeaderLib_Variables_QRY_ObjectVariableSet(_Item, "LLWEAPONEX_ParentDualShield")
AND
GetTemplate(_Item, _CombatShieldTemplate)
AND
DB_LLWEAPONEX_DualShields_Templates(_CombatShieldTemplate, _ShieldTemplate)
THEN
LeaderLog_Log("DEBUG", "[LLWEAPONEX_10_DualShields:OnItemEquipped] Parent shield not set for combat shield. Looking for shield.");
DB_LLWEAPONEX_DualShields_Temp_CombatShieldIterator(_Char, _Item);
LeaderLib_Timers_StartCharacterItemTimer(_Char, _Item, 500, "Timers_LLWEAPONEX_DualShields_FindDualShield_Failed", "LLWEAPONEX_DualShields_FindDualShield_Failed");
InventoryLaunchTemplateIterator(_Char, _ShieldTemplate, "LLWEAPONEX_Iterators_FoundDualShield_Equip", "");
//InventoryLaunchTagIterator(_Char, "LLWEAPONEX_DualShields", "", "LLWEAPONEX_Iterators_FoundDualShield_Equip", "");

IF
CharacterItemEvent(_Char, _CombatShield, "LLWEAPONEX_DualShields_FindDualShield_Failed")
AND
DB_LLWEAPONEX_DualShields_Temp_CombatShieldIterator(_Char, _CombatShield)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_CombatShieldIterator(_Char, _CombatShield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX_DualShields_FindDualShield_Failed] Combat shield's dual shield not found.");

IF
StoryEvent((ITEMGUID)_Shield, "LLWEAPONEX_Iterators_FoundDualShield_Equip")
AND
NOT LeaderLib_Variables_QRY_ObjectVariableSet(_Shield, "LLWEAPONEX_CombatShield")
AND
DB_LLWEAPONEX_DualShields_Temp_CombatShieldIterator(_Char, _CombatShield)
AND
_CombatShield != _Shield
AND
IsTagged(_Shield, "LLWEAPONEX_DualShields", 1)
THEN
NOT DB_LLWEAPONEX_DualShields_Temp_CombatShieldIterator(_Char, _CombatShield);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:DualShields:LLWEAPONEX_Iterators_FoundDualShield] Found combat shield in dual shield inventory. Setting variable & equipping.");
LeaderLib_Timers_CancelCharacterItemTimerForCharacter(_Char, "Timers_LLWEAPONEX_DualShields_FindDualShield_Failed");
SetVarObject(_Shield, "LLWEAPONEX_CombatShield", _CombatShield);
SetVarObject(_CombatShield, "LLWEAPONEX_ParentDualShield", _Shield);
//LeaderLib_Timers_StartCharacterItemTimer(_Char, _Shield, 50, "Timers_LLWEAPONEX_LeaderLib_Commands_EquipItem", "LeaderLib_Commands_EquipItem");
NRD_CharacterEquipItem(_Char, _Shield, "Shield", 0, 0, 1, 1);
NRD_CharacterEquipItem(_Char, _CombatShield, "Weapon", 0, 0, 1, 1);
//END_REGION

//REGION RUSH_PROTECTION
IF
StoryEvent(_Char, "LLWEAPONEX_ResetDualShieldsRushProtection")
THEN
ObjectClearFlag(_Char, "LLWEAPONEX_MasteryBonus_RushProtection", 0);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"