Version 1
SubGoalCombiner SGC_AND
INITSECTION
KBSECTION

//REGION HELPERS
QRY
LLWEAPONEX_Statuses_QRY_ClampTurns((CHARACTERGUID)_Target, (STRING)_Status, (INTEGER)_Min)
AND
GetStatusTurns(_Target, _Status, _Turns)
AND
IntegerMax(_Min, _Turns, _Max)
THEN
DB_LLWEAPONEX_Statuses_Temp_ClampedTurns(_Target, _Status, _Max);

QRY
LLWEAPONEX_Statuses_QRY_ClampTurns((CHARACTERGUID)_Target, (STRING)_Status, (INTEGER)_Min)
AND
NOT DB_LLWEAPONEX_Statuses_Temp_ClampedTurns(_Target, _Status, _)
THEN
DB_LLWEAPONEX_Statuses_Temp_ClampedTurns(_Target, _Status, _Min);

PROC
LLWEAPONEX_Statuses_ApplyRealStatus((CHARACTERGUID)_Target, (GUIDSTRING)_Source, (STRING)_TriggerStatus, (STRING)_RealStatus)
AND
LLWEAPONEX_Statuses_QRY_ClampTurns(_Target, _TriggerStatus, 1)
AND
DB_LLWEAPONEX_Statuses_Temp_ClampedTurns(_Target, _Status, _Turns)
AND
Real(_Turns, _Duration)
THEN
NOT DB_LLWEAPONEX_Statuses_Temp_ClampedTurns(_Target, _Status, _Turns);
ApplyStatus(_Target, _RealStatus, _Duration, 1, _Source);
RemoveStatus(_Target, _TriggerStatus);
//END_REGION

//REGION LISTEN_FOR_REMOVAL
PROC
LLWEAPONEX_Statuses_ListenForRemoval((CHARACTERGUID)_Source, (GUIDSTRING)_Target, (STRING)_Status)
AND
NOT DB_LLWEAPONEX_MasteryBonus_ListenForRemoval(_, _Target, _)
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Target, "LLWEAPONEX_ListenForStatusRemoval", "WeaponExpansion");

PROC
LLWEAPONEX_Statuses_ListenForRemoval((CHARACTERGUID)_Source, (GUIDSTRING)_Target, (STRING)_Status)
THEN
DB_LLWEAPONEX_MasteryBonus_ListenForRemoval(_Source, _Target, _Status);

PROC
LLWEAPONEX_Statuses_OnStatusRemoved((CHARACTERGUID)_Source, (GUIDSTRING)_Target, (STRING)_Status)
AND
NOT DB_LLWEAPONEX_MasteryBonus_ListenForRemoval(_, _Target, _)
THEN
LeaderLib_ToggleScripts_DisableScriptForObject(_Target, "LLWEAPONEX_ListenForStatusRemoval", "WeaponExpansion");
//END_REGION

//REGION LISTEN_FOR_TURN_ENDING
PROC
LLWEAPONEX_Statuses_ListenForTurnEnding((CHARACTERGUID)_Source, (GUIDSTRING)_Target, (STRING)_Status, (STRING)_Group)
THEN
DB_LLWEAPONEX_Statuses_ListenForTurnEnding(_Source, _Target, _Status, _Group);
LeaderLib_ToggleScripts_EnableScriptForObject(_Source, "LLWEAPONEX_ListenForTurnEnding", "WeaponExpansion");

PROC
LLWEAPONEX_Statuses_TurnEnded_OnStatusRemoved((CHARACTERGUID)_Source, (GUIDSTRING)_Target, (STRING)_Status, (STRING)_Group)
THEN
DB_NOOP(1);
//END_REGION

//REGION DEATH_SENTENCE
IF
CharacterStatusApplied(_Enemy, "LLWEAPONEX_DEATH_SENTENCE", (CHARACTERGUID)_Source)
THEN
DB_LLWEAPONEX_Statuses_Temp_DeathSentence(_Enemy, _Source);

IF
CharacterStatusRemoved(_Enemy, "LLWEAPONEX_DEATH_SENTENCE", _)
AND
CharacterIsDead(_Enemy, 0)
AND
DB_LLWEAPONEX_Statuses_Temp_DeathSentence(_Enemy, _Source)
THEN
NOT DB_LLWEAPONEX_Statuses_Temp_DeathSentence(_Enemy, _Source);

IF
CharacterDying(_Enemy)
AND
DB_LLWEAPONEX_Statuses_Temp_DeathSentence(_Enemy, _Source)
AND
GetPosition(_Enemy, _x, _y, _z)
THEN
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_SourceJar_Death_Impact_01", _x, _y, _z);
PlayEffectAtPosition("RS3_FX_GP_Impacts_Ghost_01", _x, _y, _z);

IF
CharacterDied(_Enemy)
AND
DB_LLWEAPONEX_Statuses_Temp_DeathSentence(_Enemy, _Source)
THEN
LLWEAPONEX_Statuses_CreateRevenant(_Enemy, _Source, "LLWEAPONEX_Revenant_Base_bb15f97e-b6bf-4648-9190-71b42a7744c4", " (Revenant)", "LLWEAPONEX_REVENANT", 12.0, 1);
NOT DB_LLWEAPONEX_Statuses_Temp_DeathSentence(_Enemy, _Source);

IF
CharacterStatusRemoved(_Character, "LLWEAPONEX_DEATH_SENTENCE", _)
AND
CharacterIsDead(_Character, 0)
AND
DB_LLWEAPONEX_Statuses_Temp_Revenants(_Character, _Revenant, _Source, "LLWEAPONEX_REVENANT")
THEN
LLWEAPONEX_Statuses_KillRevenant(_Revenant);
//END_REGION

//REGION REVENANTS_CREATE
PROC
LLWEAPONEX_Statuses_CreateRevenant((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (STRING)_Template, (STRING)_NameSuffix, (STRING)_Status, (REAL)_Duration, (INTEGER)_CopyEquipment)
AND
GetPosition(_Target, _x, _y, _z)
AND
TemporaryCharacterCreateAtPosition(_x, _y, _z, _Template, 0, _Revenant)
THEN
LLWEAPONEX_Statuses_Internal_SetupRevenant(_Revenant, _Target, _Source, _NameSuffix, _Status, _Duration, _CopyEquipment);

PROC
LLWEAPONEX_Statuses_CreateRevenantSummon((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (STRING)_NameSuffix, (STRING)_Status, (REAL)_Duration, (INTEGER)_CopyEquipment)
THEN
DB_LLWEAPONEX_Statuses_Temp_SetupRevenantSummon(_Target, _Source, _NameSuffix, _Status, _Duration, _CopyEquipment);
ApplyStatus(_Target, "LLWEAPONEX_SUMMON_REVENANT_EXPLODE", 0.0, 1, _Target);

IF
CharacterStatusApplied(_Revenant, "SUMMONING", (CHARACTERGUID)_Target)
AND
DB_LLWEAPONEX_Statuses_Temp_SetupRevenantSummon(_Target, _Source, _NameSuffix, _Status, _Duration, _CopyEquipment)
THEN
NOT DB_LLWEAPONEX_Statuses_Temp_SetupRevenantSummon(_Target, _Source, _NameSuffix, _Status, _Duration, _CopyEquipment);
LLWEAPONEX_Statuses_Internal_SetupRevenant(_Revenant, _Target, _Source, _NameSuffix, _Status, _Duration, _CopyEquipment);

PROC
LLWEAPONEX_Statuses_Internal_SetupRevenant((CHARACTERGUID)_Revenant, (CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (STRING)_NameSuffix, (STRING)_Status, (REAL)_Duration, (INTEGER)_CopyEquipment)
AND
CharacterGetLevel(_Target, _Level)
AND
CharacterGetDisplayName(_Target, _, _Name)
AND
StringConcatenate(_Name, _NameSuffix, _RevenantName)
THEN
DB_LLWEAPONEX_Statuses_Temp_Revenants(_Target, _Revenant, _Source, _Status);
SetOnStage(_Revenant, 0);
CharacterSetDetached(_Revenant, 1);
CharacterTransformAppearanceTo(_Revenant, _Target, 0, 1);

//CharacterCloneSkillsTo(_Target, _Revenant, 0);
CharacterLevelUpTo(_Revenant, _Level);

SetCanJoinCombat(_Revenant, 0);
SetCanFight(_Revenant, 0);
SetFaction(_Revenant, "Good NPC");
SetRelationIndivFactionToPlayers(_Revenant, 100);

CharacterSetHitpointsPercentage(_Revenant, 100.0);
DB_LLWEAPONEX_Statuses_Temp_WaitForFinished(_Revenant, _Target, _Source, _Status, _RevenantName, _Duration, _CopyEquipment);

LeaderLib_Helper_SafeTeleport(_Revenant, _Target, "LLWEAPONEX_Events_Revenant_SetAttached", 8.0);

IF
StoryEvent((CHARACTERGUID)_Revenant, "LLWEAPONEX_Events_Revenant_SetAttached")
AND
DB_LLWEAPONEX_Statuses_Temp_WaitForFinished(_Revenant, (CHARACTERGUID)_Target, _Source, _Status, _RevenantName, _Duration, _CopyEquipment)
THEN
CharacterSetCustomName(_Revenant, _RevenantName);
LeaderLib_Helper_CopyAbilities(_Revenant, _Target);
LeaderLib_Helper_CopyAttributes(_Revenant, _Target);
CharacterCloneSkillsTo(_Target, _Revenant, 1);

IF
StoryEvent((CHARACTERGUID)_Revenant, "LLWEAPONEX_Events_Revenant_SetAttached")
AND
DB_LLWEAPONEX_Statuses_Temp_WaitForFinished(_Revenant, (CHARACTERGUID)_Target, _Source, _Status, _RevenantName, _Duration, 1)
THEN
LeaderLib_Timers_StartCharacterCharacterTimer(_Revenant, _Target, 50, "LLWEAPONEX_Timers_Revenant_CopyEquipment", "LLWEAPONEX_Revenant_CopyEquipment");

IF
CharacterCharacterEvent(_Revenant, _Target, "LLWEAPONEX_Revenant_CopyEquipment")
THEN
LeaderLib_Helper_CopyEquipment(_Revenant, _Target);

IF
StoryEvent((CHARACTERGUID)_Revenant, "LLWEAPONEX_Events_Revenant_SetAttached")
THEN
CharacterSetDetached(_Revenant, 0);
PlayEffect(_Revenant, "RS3_FX_Char_Ghosts_Teleport_in_01", "");
LeaderLib_Timers_StartObjectTimer(_Revenant, 250, "LLWEAPONEX_Timers_RevenantReadyTimer", "LLWEAPONEX_Events_RevenantReady");

IF
StoryEvent((CHARACTERGUID)_Revenant, "LLWEAPONEX_Events_RevenantReady")
AND
DB_LLWEAPONEX_Statuses_Temp_WaitForFinished(_Revenant, _Target, _Source, _Status, _RevenantName, _Duration, _CopyEquipment)
THEN
NOT DB_LLWEAPONEX_Statuses_Temp_WaitForFinished(_Revenant, _Target, _Source, _Status, _RevenantName, _Duration, _CopyEquipment);
ApplyStatus(_Revenant, _Status, _Duration, 1, _Source);
LLWEAPONEX_Statuses_OnRevenantCreated(_Revenant, _Target, _Source, _Status);

IF
StoryEvent((CHARACTERGUID)_Revenant, "LLWEAPONEX_Events_RevenantReady")
THEN
SetCanJoinCombat(_Revenant, 1);
SetCanFight(_Revenant, 1);
SetOnStage(_Revenant, 1);

PROC
LLWEAPONEX_Statuses_OnRevenantCreated((CHARACTERGUID)_Revenant, (CHARACTERGUID)_Target, (CHARACTERGUID)_Source, "LLWEAPONEX_REVENANT")
AND
CharacterIsInCombat(_Revenant, 0)
AND
CharacterIsInCombat(_Source, 1)
THEN
EnterCombat(_Revenant, _Source);

PROC
LLWEAPONEX_Statuses_OnRevenantCreated((CHARACTERGUID)_Revenant, (CHARACTERGUID)_Target, (CHARACTERGUID)_Source, "LLWEAPONEX_REVENANT")
AND
CharacterIsInCombat(_Revenant, 0)
AND
GetClosestAlivePlayer(_Revenant, _Player, _Dist)
AND
CharacterIsInCombat(_Player, 0)
THEN
ProcCharacterFollowCharacter(_Revenant, _Player);

/*
IF
StoryEvent((CHARACTERGUID)_Revenant, "LLWEAPONEX_Events_RevenantReady")
AND
CharacterGetHostCharacter(_Host)
THEN
//CharacterMakePlayer(_Revenant, _Host);
TeleportTo(_Revenant, _Host, "", 0, 1, 1);
*/

IF
CharacterStatusApplied(_Character, "RESURRECT", _)
AND
DB_LLWEAPONEX_Statuses_Temp_Revenants(_Character, _Revenant, _Source, _Status)
THEN
LLWEAPONEX_Statuses_KillRevenant(_Revenant);

IF
CharacterStatusRemoved(_Revenant, _Status, _)
AND
DB_LLWEAPONEX_Statuses_Temp_Revenants(_Enemy, _Revenant, _Source, _Status)
THEN
LLWEAPONEX_Statuses_KillRevenant(_Revenant);

PROC
LLWEAPONEX_Statuses_KillRevenant((CHARACTERGUID)_Revenant)
AND
GetPosition(_Revenant, _x, _y, _z)
THEN
SetOnStage(_Revenant, 0);
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_GhostDissipate_01", _x, _y, _z);
CharacterDieImmediate(_Revenant, 0, "LifeTime", _Revenant);

IF
CharacterDied(_Revenant)
AND
DB_LLWEAPONEX_Statuses_Temp_Revenants(_Target, _Revenant, _Source, _Status)
THEN
LLWEAPONEX_Statuses_OnRevenantDied(_Revenant, _Target, _Source, _Status);

PROC
LLWEAPONEX_Statuses_OnRevenantDied((CHARACTERGUID)_Revenant, (CHARACTERGUID)_Target, (CHARACTERGUID)_Source, "LLWEAPONEX_REVENANT")
AND
GetPosition(_Revenant, _x, _y, _z)
THEN
TransformSurfaceAtPosition(_x, _y, _z, "Vaporize", "Ground", 1.0, 12.0, _Source);
LLWEAPONEX_Statuses_RemoveRevenant(_Revenant);

PROC
LLWEAPONEX_Statuses_RemoveRevenant((CHARACTERGUID)_Revenant)
AND
DB_LLWEAPONEX_Statuses_Temp_Revenants(_Enemy, _Revenant, _Source, _Status)
THEN
NOT DB_LLWEAPONEX_Statuses_Temp_Revenants(_Enemy, _Revenant, _Source, _Status);
RemoveTemporaryCharacter(_Revenant);
//END_REGION

//REGION REVENANTS_TORMENT
PROC
LLWEAPONEX_Statuses_OnRevenantCreated((CHARACTERGUID)_Revenant, (CHARACTERGUID)_Target, (CHARACTERGUID)_Source, "LLWEAPONEX_TORMENTED_GHOST")
//AND
//DB_IsPlayer(_Target)
THEN
DB_LLWEAPONEX_Skills_Temp_AttachedRevanents(_Revenant, _Target);
CharacterAddToPlayerCharacter(_Revenant, _Target);
LeaderLib_Helper_CopyEquipment(_Revenant, _Target);

PROC
LLWEAPONEX_Statuses_OnRevenantDied((CHARACTERGUID)_Revenant, (CHARACTERGUID)_Target, (CHARACTERGUID)_Source, "LLWEAPONEX_TORMENTED_GHOST")
THEN
LLWEAPONEX_Statuses_RemoveRevenant(_Revenant);

PROC
LLWEAPONEX_Statuses_RemoveRevenant((CHARACTERGUID)_Revenant)
AND
DB_LLWEAPONEX_Skills_Temp_AttachedRevanents(_Revenant, _Target)
THEN
NOT DB_LLWEAPONEX_Skills_Temp_AttachedRevanents(_Revenant, _Target);
//END_REGION

//REGION REVENANTS_SAVE_WORKAROUND
/* Revenant Save Bug 2/4/2019
When a save with an attached revenant is loaded, that revenant no longer remains attached, despite still being a "player".
Not sure why this is happeneing, but unattaching them and re-attaching after the game is started works.
*/

//Old saves
IF
SavegameLoading(_,_,_,_)
AND
DB_LLWEAPONEX_Statuses_Temp_Revenants(_Target, _Revenant, _Source, "LLWEAPONEX_TORMENTED_GHOST")
AND
NOT DB_LLWEAPONEX_Skills_Temp_AttachedRevanents(_Revenant, _Target)
THEN
DB_LLWEAPONEX_Skills_Temp_AttachedRevanents(_Revenant, _Target);

IF
SavegameLoading(_,_,_,_)
AND
DB_LLWEAPONEX_Skills_Temp_AttachedRevanents(_Revenant, _Target)
THEN
DB_LLWEAPONEX_Statuses_Temp_ReattachRevenant(_Revenant, _Target);
CharacterRemoveFromPlayerCharacter(_Revenant, _Target);

IF
GameStarted(_,_)
AND
DB_LLWEAPONEX_Statuses_Temp_ReattachRevenant(_Revenant, _Target)
THEN
NOT DB_LLWEAPONEX_Statuses_Temp_ReattachRevenant(_Revenant, _Target);
CharacterAddToPlayerCharacter(_Revenant, _Target);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"
