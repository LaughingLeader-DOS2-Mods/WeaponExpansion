Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION CRIT_DAMAGE
IF
NRD_OnPrepareHit(_Target, (CHARACTERGUID)_Char, _Damage, _Handle)
AND
HasActiveStatus(_Char, "LLWEAPONEX_UNRELENTING_RAGE", 1)
AND
NRD_HitGetInt(_Handle, "CriticalHit", 1)
AND
LeaderLib_Hit_QRY_HitSucceeded(_Target, _Handle, 1)
AND
NRD_HitGetInt(_Handle, "DamageSourceType", _DamageSourceType)
AND
LLWEAPONEX_Helper_IntMatch(_DamageSourceType, 0, 6, 7) // Ignore surfaces/statuses/GM
AND
NRD_StatGetExtraData("LLWEAPONEX_UnrelentingRage_CriticalDamageBonus", _MultA)
AND
RealDivide(_MultA, 100.0, _CritMult)
AND
GetUUID(_Target, _UUID1)
AND
GetUUID(_Char, _UUID2)
THEN
LeaderLib_Hit_IncreaseDamage(_Target, _Char, _Handle, _CritMult, 1);
//END_REGION

//REGION DAMAGE_BONUS
IF
CharacterKilledBy(_Target, _, _Char)
AND
HasActiveStatus(_Char, "LLWEAPONEX_UNRELENTING_RAGE", 1)
AND
CharacterIsAlly(_Target, _Char, 0)
THEN
ApplyStatus(_Char, "LLWEAPONEX_UNRELENTING_RAGE_BONUS_APPLY", 0.0, 0, _Char);

IF
CharacterStatusRemoved(_Char, "LLWEAPONEX_UNRELENTING_RAGE", _)
THEN
ApplyStatus(_Char, "LLWEAPONEX_UNRELENTING_RAGE_BONUS_REMOVE", 0.0, 0, _Char);
//END_REGION

//REGION DISABLING_STATUS
PROC
LeaderLib_StatusEvents_OnDisablingStatusApplied((CHARACTERGUID)_Char, (GUIDSTRING)_Cause, (STRING)_Status, (STRING)_Type)
AND
HasActiveStatus(_Char, "LLWEAPONEX_UNRELENTING_RAGE", 1)
THEN
RemoveStatus(_Char, "LLWEAPONEX_UNRELENTING_RAGE");
//END_REGION

//REGION MUST_ATTACK
IF
ObjectTurnStarted((CHARACTERGUID)_Char)
AND
HasActiveStatus(_Char, "LLWEAPONEX_UNRELENTING_RAGE", 1)
THEN
DB_LLWEAPONEX_Statuses_Temp_Rage_ListenForAttacked(_Char);

IF
AttackedByObject(_, _, (CHARACTERGUID)_Char, _, _)
AND
DB_LLWEAPONEX_Statuses_Temp_Rage_ListenForAttacked(_Char)
THEN
NOT DB_LLWEAPONEX_Statuses_Temp_Rage_ListenForAttacked(_Char);

IF
ObjectTurnEnded((CHARACTERGUID)_Char)
AND
DB_LLWEAPONEX_Statuses_Temp_Rage_ListenForAttacked(_Char)
THEN
NOT DB_LLWEAPONEX_Statuses_Temp_Rage_ListenForAttacked(_Char);
RemoveStatus(_Char, "LLWEAPONEX_UNRELENTING_RAGE");
//END_REGION

EXITSECTION
SysClear("DB_LLWEAPONEX_Statuses_Temp_Rage_ListenForAttacked", 1);
ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_80_ToggledScripts"