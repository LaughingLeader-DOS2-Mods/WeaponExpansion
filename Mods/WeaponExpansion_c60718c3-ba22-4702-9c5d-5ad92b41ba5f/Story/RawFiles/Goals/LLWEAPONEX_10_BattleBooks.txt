Version 1
SubGoalCombiner SGC_AND
INITSECTION
KBSECTION

PROC
LeaderLib_Mods_OnVersionChanged(c60718c3-ba22-4702-9c5d-5ad92b41ba5f, (INTEGER)_LastVersion, (INTEGER)_NextVersion)
AND
_LastVersion < 152633346
THEN
LeaderLib_Randomization_Clear_Table("LLWEAPONEX_BattleBooks_SpellScroll_Spells", 1);
SysClear("DB_LLWEAPONEX_BattleBooks_SpellScroll_SpellNames", 2);
SysClear("DB_LLWEAPONEX_BattleBooks_SpellScroll_CasterEffects", 3);
SysClear("DB_LLWEAPONEX_BattleBooks_SpellScroll_ExpodeEffects", 3);
SysClear("DB_LLWEAPONEX_BattleBooks_SpellScroll_ProjectileSettings", 3);

//WPN_UNIQUE_LLWEAPONEX_BattleBook_2H_Spellbook_A_ecb56a2b-0bfa-40d7-ba92-525c2aab0ae1
//WPN_UNIQUE_LLWEAPONEX_BattleBook_2H_SpellScroll_A_f393c9d6-d90c-483f-be94-b47e9419395d
//REGION SPELLBOOK
PROC
LLWEAPONEX_OnItemTemplateEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "WPN_UNIQUE_LLWEAPONEX_BattleBook_2H_SpellScroll_A_f393c9d6-d90c-483f-be94-b47e9419395d")
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Char, "LLWEAPONEX_BattleBooks_SpellScrollEquipped", "WeaponExpansion");

PROC
LLWEAPONEX_OnItemTemplateEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "WPN_UNIQUE_LLWEAPONEX_BattleBook_2H_SpellScroll_A_f393c9d6-d90c-483f-be94-b47e9419395d")
AND
GetUUID(_Char, _UUID)
AND
GetUUID(_Item, _ItemUUID)
THEN
NRD_ModCall("WeaponExpansion", "OnSpellScrollEquipped", _UUID, _ItemUUID);

PROC
LLWEAPONEX_OnItemTemplateUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "WPN_UNIQUE_LLWEAPONEX_BattleBook_2H_SpellScroll_A_f393c9d6-d90c-483f-be94-b47e9419395d")
THEN
LeaderLib_ToggleScripts_DisableScriptForObjectAfterDelay(_Char, "LLWEAPONEX_BattleBooks_SpellScrollEquipped", "WeaponExpansion", 500);

PROC
LLWEAPONEX_OnItemTemplateUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "WPN_UNIQUE_LLWEAPONEX_BattleBook_2H_SpellScroll_A_f393c9d6-d90c-483f-be94-b47e9419395d")
AND
GetUUID(_Char, _UUID)
AND
GetUUID(_Item, _ItemUUID)
THEN
NRD_ModCall("WeaponExpansion", "OnSpellScrollUnequipped", _UUID, _ItemUUID);

/*
IF
ItemDropped(_Item)
AND
GetTemplate(_Item, "WPN_UNIQUE_LLWEAPONEX_BattleBook_2H_SpellScroll_A_f393c9d6-d90c-483f-be94-b47e9419395d")
THEN
PlayEffect(_Item, "LLWEAPONEX_FX_Status_SpellScroll_ElementShifted_Item_01");
*/
//END_REGION

//REGION SPELLBOOK_RANDOM_SPELL
IF
CharacterStatusApplied(_Obj, "LLWEAPONEX_BATTLEBOOK_SPELLSCROLL_FX_DIMENSIONALBOLT", _)
THEN
ProcObjectTimerCancel(_Obj, "LLWEAPONEX_Timers_BattleBooks_SpellScroll_RemoveBeamStatuses");
ProcObjectTimer(_Obj, "LLWEAPONEX_Timers_BattleBooks_SpellScroll_RemoveBeamStatuses", 500);

IF
CharacterStatusApplied(_Obj, "LLWEAPONEX_BATTLEBOOK_SPELLSCROLL_FX_TENTACLE", _)
THEN
ProcObjectTimerCancel(_Obj, "LLWEAPONEX_Timers_BattleBooks_SpellScroll_RemoveBeamStatuses");
ProcObjectTimer(_Obj, "LLWEAPONEX_Timers_BattleBooks_SpellScroll_RemoveBeamStatuses", 500);

IF
ItemStatusChange(_Obj, "LLWEAPONEX_BATTLEBOOK_SPELLSCROLL_FX_DIMENSIONALBOLT", _)
THEN
ProcObjectTimerCancel(_Obj, "LLWEAPONEX_Timers_BattleBooks_SpellScroll_RemoveBeamStatuses");
ProcObjectTimer(_Obj, "LLWEAPONEX_Timers_BattleBooks_SpellScroll_RemoveBeamStatuses", 500);

IF
ItemStatusChange(_Obj, "LLWEAPONEX_BATTLEBOOK_SPELLSCROLL_FX_TENTACLE", _)
THEN
ProcObjectTimerCancel(_Obj, "LLWEAPONEX_Timers_BattleBooks_SpellScroll_RemoveBeamStatuses");
ProcObjectTimer(_Obj, "LLWEAPONEX_Timers_BattleBooks_SpellScroll_RemoveBeamStatuses", 500);

PROC
ProcObjectTimerFinished(_Obj, "LLWEAPONEX_Timers_BattleBooks_SpellScroll_RemoveBeamStatuses")
THEN
RemoveStatus(_Obj, "LLWEAPONEX_BATTLEBOOK_SPELLSCROLL_FX_DIMENSIONALBOLT");
RemoveStatus(_Obj, "LLWEAPONEX_BATTLEBOOK_SPELLSCROLL_FX_TENTACLE");
//END_REGION

//REGION BOOK_XP
IF
CharacterUsedItemTemplate(_Player, _BookTemplate, _Book)
AND
NOT DB_LLWEAPONEX_BattleBooks_ReadBooks(_Player, _BookTemplate)
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Book, "BOOK", 1)
THEN
DB_LLWEAPONEX_BattleBooks_ReadBooks(_Player, _BookTemplate);
LLWEAPONEX_Ext_AddMasteryExperience(_Player, "LLWEAPONEX_BattleBook", 0.25);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"