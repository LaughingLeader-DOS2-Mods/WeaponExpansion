Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_Greatbows_InitSettings();

KBSECTION
//REGION SETTINGS
PROC
LLWEAPONEX_Greatbows_InitSettings()
THEN
/*
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_DWARF", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarves_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_DWARF", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarves_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_ELF", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elves_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_ELF", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elves_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_HUMAN", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Humans_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_HUMAN", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Humans_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_LIZARD", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizards_01", "Dummy_R_Hand", 900);
DB_LLWEAPONEX_Greatbows_PrepareFX("UNDEAD_LIZARD", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizards_01", "Dummy_R_Hand", 900);

DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", "Undead_Dwarf", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarf_Female", "LowerArm_R_Twist_Bone", 500);
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", "Undead_Dwarf", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarf_Male", "LowerArm_R_Twist_Bone", 500);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", "Undead_Elf", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elf_Female", "Dummy_R_Hand", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", "Undead_Elf", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elf_Male", "Dummy_R_Hand", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", "Undead_Human", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Human_Female", "Dummy_R_Hand", 800);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", "Undead_Human", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Human_Male", "Dummy_R_Hand", 800);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", "Undead_Lizard", 1,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizard_Female", "LowerArm_R_Twist_Bone", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", "Undead_Lizard", 0,  "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizard_Male", "LowerArm_R_Twist_Bone", 600);
*/

SysClear("DB_LLWEAPONEX_Greatbows_PrepareFX", 5);
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 1, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarf_Female", "LowerArm_R_Twist_Bone", 425);
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 0, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Dwarf_Male", "LowerArm_R_Twist_Bone", 425);
//DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 1, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Human_Female", "Dummy_R_Hand", 500);
//DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 0, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Human_Male", "Dummy_R_Hand", 400);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", 1, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elf_Female", "Dummy_R_Hand", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", 0, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Elf_Male", "Dummy_R_Hand", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", 1, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Human_Female", "Dummy_R_Hand", 825);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", 0, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Human_Male", "Dummy_R_Hand", 825);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", 1, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizard_Female", "LowerArm_R_Twist_Bone", 580);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", 0, "Default", "LLWEAPONEX_FX_Projectiles_Greatbow_Arrow_Prepare_Lizard_Male", "LowerArm_R_Twist_Bone", 600);

//Omnibolt Greatbow
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Dwarf_Female", "LowerArm_R_Twist_Bone", 425);
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Dwarf_Male", "LowerArm_R_Twist_Bone", 425);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Elf_Female", "Dummy_R_Hand", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Elf_Male", "Dummy_R_Hand", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Human_Female", "Dummy_R_Hand", 825);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Human_Male", "Dummy_R_Hand", 825);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Lizard_Female", "LowerArm_R_Twist_Bone", 580);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Lizard_Male", "LowerArm_R_Twist_Bone", 600);

/*
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 500);
DB_LLWEAPONEX_Greatbows_PrepareFX("DWARF", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 500);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("ELF", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 600);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 825);
DB_LLWEAPONEX_Greatbows_PrepareFX("HUMAN", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 825);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", 1, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 580);
DB_LLWEAPONEX_Greatbows_PrepareFX("LIZARD", 0, "Lightning", "LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Prepare_Hand_01", "Dummy_R_HandFX", 600);
*/

//Omnibolt
DB_LLWEAPONEX_Greatbows_TemplateToID("WPN_UNIQUE_LLWEAPONEX_Greatbow_Lightning_Bow_2H_A_7efec0e0-1c2e-4f0d-9ec5-e3a1f40c97b8", "Lightning");

SysClear("DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage", 3);
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(0.0, 1.2, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE1");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(1.2, 2.4, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE2");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(2.4, 3.6, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE3");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(3.6, 4.8, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE4");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(4.8, 6.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE5");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(6.0, 7.2, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE6");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(7.2, 9.4, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE7");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(9.4, 11.5, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE8");
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(11.5, 999.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_DIST_DAMAGE9");

SysClear("DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage", 3);
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(0.25, 2.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE1");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(2.0, 3.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE2");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(3.0, 4.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE3");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(4.0, 5.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE4");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(5.0, 9.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE5");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(9.0, 12.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE6");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(12.0, 16.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE7");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(16.0, 19.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE8");
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(19.0, 999.0, "LLWEAPONEX_GREATBOW_CUSHIONFORCE_FALL_DAMAGE9");

SysClear("DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance", 3);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(0, -999.0, 5.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(1, 5.0, 6.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(2, 6.0, 7.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(3, 7.0, 8.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(4, 8.0, 9.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(5, 9.0, 10.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(6, 10.0, 11.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(7, 11.0, 12.0);
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(8, 12.0, 999.0);
//END_REGION

//REGION UPDATES
PROC
LeaderLib_Mods_OnVersionChanged(c60718c3-ba22-4702-9c5d-5ad92b41ba5f, (INTEGER)_LastVersion, (INTEGER)_NextVersion)
THEN
LLWEAPONEX_Greatbows_InitSettings();
//END_REGION

//REGION EQUIPPED_DB
PROC
LLWEAPONEX_Equipment_OnTaggedItemEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "LLWEAPONEX_Greatbow", (INTEGER)_IsPlayer)
AND
GetTemplate(_Item, _Template)
THEN
DB_LLWEAPONEX_Greatbows_Temp_Equipped(_Char, _Item, _Template);

PROC
LLWEAPONEX_Equipment_OnTaggedItemUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, "LLWEAPONEX_Greatbow", (INTEGER)_IsPlayer)
AND
DB_LLWEAPONEX_Greatbows_Temp_Equipped(_Char, _Item, _Template)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_Equipped(_Char, _Item, _Template);

IF
RegionEnded(_)
AND
DB_LLWEAPONEX_Greatbows_Temp_Equipped(_Char, _Item, _Template)
AND
ObjectIsGlobal(_Char, 0)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_Equipped(_Char, _Item, _Template);

IF
RegionStarted(_)
AND
DB_LLWEAPONEX_Greatbows_Temp_Equipped(_Char, _Item, _Template)
AND
ObjectExists(_Char, 0)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_Equipped(_Char, _Item, _Template);
//END_REGION

//REGION GREATBOW_START_ATTACK
IF
CharacterUsedSkill(_Char, "Projectile_LLMIME_MimicGreatBowAttack", _, _)
THEN
LLWEAPONEX_Greatbows_PlayPreparationEffect(_Char, "Default");

/*
PROC
LLWEAPONEX_Greatbows_StartPlayingPreparationEffect((CHARACTERGUID)_Char, (STRING)_Template)
AND
DB_LLWEAPONEX_Greatbows_TemplateToID(_Template, "Lightning")
THEN
//LLWEAPONEX_Greatbows_PlayPreparationEffect(_Char, _ID);
PROC_StopLoopBeamEffect(_Char, "LLWEAPONEX.FX.GreatbowAttackFX");
PROC_LoopBeamEffect("LLWEAPONEX_FX_Projectiles_Greatbow_Lightning_Beam_01", _Char, _Char, "Dummy_R_Hand", "Dummy_ProjectileFX", "LLWEAPONEX.FX.GreatbowAttackFX", "__ANY__");
*/

PROC
LLWEAPONEX_Greatbows_StartPlayingPreparationEffect((CHARACTERGUID)_Char, (STRING)_Template)
AND
DB_LLWEAPONEX_Greatbows_TemplateToID(_Template, _ID)
THEN
//LeaderLog_Log("DEBUG", "[LLWEAPONEX_10_Greatbows:StartPlayingPreparationEffect] Playing prepare effect with ID [",_ID,"] from template [",_Template,"].");
LLWEAPONEX_Greatbows_PlayPreparationEffect(_Char, _ID);

PROC
LLWEAPONEX_Greatbows_StartPlayingPreparationEffect((CHARACTERGUID)_Char, (STRING)_Template)
AND
NOT DB_LLWEAPONEX_Greatbows_TemplateToID(_Template, _)
THEN
//LeaderLog_Log("DEBUG", "[LLWEAPONEX_10_Greatbows:StartPlayingPreparationEffect] No ID found for template [",_Template,"]. Using default.");
LLWEAPONEX_Greatbows_PlayPreparationEffect(_Char, "Default");

//Players only. CharacterGetRace doesn't work with NPCs.
PROC
LLWEAPONEX_Greatbows_PlayPreparationEffect((CHARACTERGUID)_Char, (STRING)_ID)
AND
NOT DB_LoopEffect(_Char, _, "LLWEAPONEX.FX.GreatbowAttackFX", _, _, _)
AND
IsTagged(_Char, "FEMALE", _IsFemale)
AND
CharacterGetRace(_Char, 1, _Race)
AND
DB_LeaderLib_RaceTags(_Race, _RaceTag, _BaseRaceTag)
AND
DB_LLWEAPONEX_Greatbows_PrepareFX(_BaseRaceTag, _IsFemale, _ID, _EffectName, _BoneName, _Duration)
THEN
//Play the loop FX with behavior scripting so we can destroy the effect immediately
//LeaderLog_Log("TRACE", "[LLWEAPONEX:Greatbows:PlayPreparationEffect] Playing arrow FX on attacking character. [",_EffectName,":",_BoneName,"]");
PROC_StopLoopEffect(_Char, "LLWEAPONEX.FX.GreatbowAttackFX");
PROC_LoopEffect(_EffectName, _Char, "LLWEAPONEX.FX.GreatbowAttackFX", "__ANY__", _BoneName);
ProcObjectTimer(_Char, "LLWEAPONEX_Timers_StopGreatbowPrepareFX", _Duration);

//NPCs
PROC
LLWEAPONEX_Greatbows_PlayPreparationEffect((CHARACTERGUID)_Char, (STRING)_ID)
AND
NOT DB_LoopEffect(_Char, _, "LLWEAPONEX.FX.GreatbowAttackFX", _, _, _)
AND
IsTagged(_Char, "FEMALE", _IsFemale)
AND
DB_LeaderLib_RaceTags(_Race, _RaceTag, _BaseRaceTag)
AND
IsTagged(_Char, _RaceTag, 1)
AND
DB_LLWEAPONEX_Greatbows_PrepareFX(_BaseRaceTag, _IsFemale, _ID, _EffectName, _BoneName, _Duration)
AND
NOT DB_LoopEffect(_Char, _, "LLWEAPONEX.FX.GreatbowAttackFX", _, _, _)
THEN
//Play the loop FX with behavior scripting so we can destroy the effect immediately
//LeaderLog_Log("TRACE", "[LLWEAPONEX:Greatbows:PlayPreparationEffect] Playing arrow FX on attacking character. [",_EffectName,":",_BoneName,"]");
PROC_StopLoopEffect(_Char, "LLWEAPONEX.FX.GreatbowAttackFX");
PROC_LoopEffect(_EffectName, _Char, "LLWEAPONEX.FX.GreatbowAttackFX", "__ANY__", _BoneName);
ProcObjectTimer(_Char, "LLWEAPONEX_Timers_StopGreatbowPrepareFX", _Duration);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "LLWEAPONEX_Timers_StopGreatbowPrepareFX")
THEN
//LeaderLog_Log("TRACE", "[LLWEAPONEX:Greatbows:StopGreatbowPrepareFX] Destroying looped arrow effect.");
PROC_StopLoopEffect(_Char, "LLWEAPONEX.FX.GreatbowAttackFX");
//END_REGION

//REGION BONUS_EFFECTS
PROC
LLWEAPONEX_Greatbows_OnAttack((CHARACTERGUID)_Char, (STRING)_Template, (GUIDSTRING)_Target)
THEN
DB_NOOP(1);

/*
PROC
LLWEAPONEX_Greatbows_OnAttackAtPosition((CHARACTERGUID)_Char, (STRING)_Template, (REAL)_x, (REAL)_y, (REAL)_z)
THEN
DB_NOOP(1);
*/

/*
PROC
LLWEAPONEX_Greatbows_OnAttack((CHARACTERGUID)_Char, "WPN_UNIQUE_LLWEAPONEX_Greatbow_Lightning_Bow_2H_A_7efec0e0-1c2e-4f0d-9ec5-e3a1f40c97b8", (GUIDSTRING)_Target)
AND
LeaderLib_Roll_QRY(450, 999)
THEN
ApplyStatus(_Target, "LLWEAPONEX_GREATBOW_LIGHTNINGSTRIKE", 0.0, 0, _Char);
*/


QRY
LLWEAPONEX_Greatbows_QRY_GetAttackDelay((CHARACTERGUID)_Char, (STRING)_ID)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_AttackDelay(_Char, _)
AND
IsTagged(_Char, "FEMALE", _IsFemale)
AND
CharacterGetRace(_Char, 1, _Race)
AND
DB_LeaderLib_RaceTags(_Race, _RaceTag, _BaseRaceTag)
AND
DB_LLWEAPONEX_Greatbows_PrepareFX(_BaseRaceTag, _IsFemale, _ID, _EffectName, _BoneName, _AttackDelay)
THEN
DB_LLWEAPONEX_Greatbows_Temp_AttackDelay(_Char, _AttackDelay);

QRY
LLWEAPONEX_Greatbows_QRY_GetAttackDelay((CHARACTERGUID)_Char, (STRING)_ID)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_AttackDelay(_Char, _)
AND
IsTagged(_Char, "FEMALE", _IsFemale)
AND
NOT CharacterGetRace(_Char, 1, _)
AND
DB_LeaderLib_RaceTags(_Race, _RaceTag, _BaseRaceTag)
AND
IsTagged(_Char, _RaceTag, 1)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_AttackDelay(_Char, _)
AND
DB_LLWEAPONEX_Greatbows_PrepareFX(_BaseRaceTag, _IsFemale, _ID, _EffectName, _BoneName, _AttackDelay)
THEN
DB_LLWEAPONEX_Greatbows_Temp_AttackDelay(_Char, _AttackDelay);
//END_REGION

//REGION OMNIBOLT
//Basic attack position summon lightning
PROC
LLWEAPONEX_Greatbows_OnAttackAtPosition((CHARACTERGUID)_Char, "WPN_UNIQUE_LLWEAPONEX_Greatbow_Lightning_Bow_2H_A_7efec0e0-1c2e-4f0d-9ec5-e3a1f40c97b8", (REAL)_x, (REAL)_y, (REAL)_z)
AND
LeaderLib_Roll_QRY(299)
AND
GetDistanceToPosition(_Char, _x, _y, _z, _Dist)
AND
RealProduct(_Dist, 85.0, _DelayR)
AND
Integer(_DelayR, _DelayInt)
AND
LLWEAPONEX_Greatbows_QRY_GetAttackDelay(_Char, "Lightning")
AND
DB_LLWEAPONEX_Greatbows_Temp_AttackDelay(_Char, _AttackDelay)
AND
IntegerSum(_DelayInt, _AttackDelay, _Delay)
AND
IntegerMax(_Delay, 1300, _DelayClamped)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_AttackDelay(_Char, _AttackDelay);
//LeaderLog_LogInt("DEBUG", "[LLWEAPONEX_10_Greatbows:OnAttackAtPosition] Shooting lightning bolt at position after [", _DelayClamped, "] ms.");
SetVarFloat3(_Char, "LLWEAPONEX_Greatbow_LightningStrikePosition", _x, _y, _z);
LeaderLib_Timers_StartObjectTimer(_Char, _DelayClamped, "LLWEAPONEX_Timers_Greatbow_Greatbow_ExplodeLightningStrikeAtPosition", "LLWEAPONEX_Greatbow_ExplodeLightningStrikeAtPosition");
//END_REGION

//REGION CUSHION_FORCE
IF
CharacterStatusApplied(_Target, "LLWEAPONEX_GREATBOW_CUSHION_FORCE", (CHARACTERGUID)_Source)
AND
_Source != NULL_00000000-0000-0000-0000-000000000000
AND
GetPosition(_Target, _x, _y, _z)
AND
GetDistanceTo(_Target, _Source, _Dist)
AND
RealSubtract(12.0, _Dist, _ForceDist) // Closer == Launch farther
THEN
LLWEAPONEX_Greatbows_Internal_CushionForce_DetermineForceIndex(_Target, _Source, _ForceDist);
DB_LLWEAPONEX_Greatbows_Temp_CushionForce(_Target, _Source, _x, _y, _z);
CharacterCharacterSetEvent(_Source, _Target, "LLWEAPONEX_Greatbow_CushionForce_StartLaunch");

PROC
LLWEAPONEX_Greatbows_Internal_CushionForce_DetermineForceIndex((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (REAL)_ForceDist)
AND
DB_LLWEAPONEX_Greatbows_CushionForce_ForceDistance(_Index, _Min, _Max)
AND
NOT DB_LLWEAPONEX_Greatbows_CushionForce_Temp_ForceResolved(_Target, _Source)
AND
_ForceDist >= _Min
AND
_ForceDist < _Max
THEN
DB_LLWEAPONEX_Greatbows_CushionForce_Temp_ForceResolved(_Target, _Source);
SetVarInteger(_Source, "LLWEAPONEX_Force_ForceIndex", _Index);

PROC
LLWEAPONEX_Greatbows_Internal_CushionForce_DetermineForceIndex((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (REAL)_ForceDist)
AND
DB_LLWEAPONEX_Greatbows_CushionForce_Temp_ForceResolved(_Target, _Source)
THEN
NOT DB_LLWEAPONEX_Greatbows_CushionForce_Temp_ForceResolved(_Target, _Source);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Target, "LLWEAPONEX_Timers_CushionForce_CheckIsFalling")
AND
DB_LLWEAPONEX_Greatbows_Temp_CushionForce_Landing(_Target, _Source, _x, _y, _z, _tx, _ty, _tz)
AND
GetPosition(_Target, _cx, _cy, _cz)
AND
RealSubtract(_ty, _cy, _HeightDiff)
AND
LeaderLib_Math_QRY_AbsoluteValue(_HeightDiff)
AND
DB_LeaderLib_Math_AbsoluteValue(_HeightDiff, _HeightDiffAbs)
THEN
NOT DB_LeaderLib_Math_AbsoluteValue(_HeightDiff, _HeightDiffAbs);
NOT DB_LLWEAPONEX_Greatbows_Temp_CushionForce_Landing(_Target, _Source, _x, _y, _z, _tx, _ty, _tz);
DB_LLWEAPONEX_Greatbows_Temp_CushionForce_Landing(_Target, _Source, _x, _y, _z, _cx, _cy, _cz);
LLWEAPONEX_Greatbows_Internal_CushionForce_CheckForFalling(_Target, _HeightDiffAbs);

PROC
LLWEAPONEX_Greatbows_Internal_CushionForce_CheckForFalling((CHARACTERGUID)_Target, (REAL)_HeightDiff)
AND
_HeightDiff > 0.01
THEN
ProcObjectTimerCancel(_Target, "LLWEAPONEX_Timers_CushionForce_CheckIsFalling");
ProcObjectTimer(_Target, "LLWEAPONEX_Timers_CushionForce_CheckIsFalling", 48);

PROC
LLWEAPONEX_Greatbows_Internal_CushionForce_CheckForFalling((CHARACTERGUID)_Target, (REAL)_HeightDiff)
AND
_HeightDiff <= 0.01
AND
DB_LLWEAPONEX_Greatbows_Temp_CushionForce_Landing(_Target, _Source, _x, _y, _z, _tx, _ty, _tz)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_CushionForce_Landing(_Target, _Source, _x, _y, _z, _tx, _ty, _tz);
RemoveStatus(_Target, "LLWEAPONEX_GREATBOW_CUSHION_FORCE");
LLWEAPONEX_Greatbows_Internal_DistanceDamage_Start(_Target, _Source, _x, _y, _z);

PROC
LLWEAPONEX_Greatbows_Internal_DistanceDamage_Start((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (REAL)_x, (REAL)_y, (REAL)_z)
AND
LeaderLib_Combat_QRY_IsEnemy(_Target, _Source) // Checks for friendly fire
AND
GetPosition(_Target, _tx, _ty, _tz)
AND
GetDistanceToPosition(_Target, _x, _y, _z, _Dist)
AND
RealSubtract(_ty, _y, _FallDistance)
AND
LeaderLib_Math_QRY_AbsoluteValue(_FallDistance)
AND
DB_LeaderLib_Math_AbsoluteValue(_FallDistance, _FallDistanceAbs)
THEN
NOT DB_LeaderLib_Math_AbsoluteValue(_FallDistance, _FallDistanceAbs);
LLWEAPONEX_Greatbows_Internal_DistanceDamage_Apply(_Target, _Source, _Dist);
LLWEAPONEX_Greatbows_Internal_FallDamage_Apply(_Target, _Source, _FallDistanceAbs);

PROC
LLWEAPONEX_Greatbows_Internal_DistanceDamage_Apply((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (REAL)_TotalDist)
AND
DB_LLWEAPONEX_Greatbows_CushionForce_DistanceDamage(_Min, _Max, _Status)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_DistanceDamageResolved(_Target, _Source)
AND
_TotalDist >= _Min
AND
_TotalDist <= _Max
THEN
DB_LLWEAPONEX_Greatbows_Temp_DistanceDamageResolved(_Target, _Source);
ApplyStatus(_Target, _Status, 0.0, 0, _Source);

PROC
LLWEAPONEX_Greatbows_Internal_DistanceDamage_Apply((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (REAL)_TotalDist)
AND
DB_LLWEAPONEX_Greatbows_Temp_DistanceDamageResolved(_Target, _Source)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_DistanceDamageResolved(_Target, _Source);

PROC
LLWEAPONEX_Greatbows_Internal_FallDamage_Apply((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (REAL)_FallDistance)
AND
_FallDistance >= 0.25
AND
DB_LLWEAPONEX_Greatbows_CushionForce_FallDamage(_Min, _Max, _Status)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_FallDamageResolved(_Target, _Source)
AND
_FallDistance >= _Min
AND
_FallDistance <= _Max
THEN
DB_LLWEAPONEX_Greatbows_Temp_FallDamageResolved(_Target, _Source);
ApplyStatus(_Target, _Status, 0.0, 0, _Source);

PROC
LLWEAPONEX_Greatbows_Internal_FallDamage_Apply((CHARACTERGUID)_Target, (CHARACTERGUID)_Source, (REAL)_FallDistance)
AND
DB_LLWEAPONEX_Greatbows_Temp_FallDamageResolved(_Target, _Source)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_FallDamageResolved(_Target, _Source);
//END_REGION

//REGION CUSHION_FORCE_LANDING
IF
CharacterStatusRemoved(_Target, "SHOCKWAVE", _)
AND
DB_LLWEAPONEX_Greatbows_Temp_CushionForce(_Target, _Source, _x, _y, _z)
AND
GetPosition(_Target, _tx, _ty, _tz)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_CushionForce(_Target, _Source, _x, _y, _z);
DB_LLWEAPONEX_Greatbows_Temp_CushionForce_Landing(_Target, _Source, _x, _y, _z, _tx, _ty, _tz);
ProcObjectTimerCancel(_Target, "LLWEAPONEX_Timers_CushionForce_CheckIsFalling");
ProcObjectTimer(_Target, "LLWEAPONEX_Timers_CushionForce_CheckIsFalling", 60);
//END_REGION

//REGION DELAYED_BARRAGE
PROC
LeaderLib_Turns_OnPositionObjectTurnChanged((STRING)_ID, (GUIDSTRING)_Char, (REAL)_x, (REAL)_y, (REAL)_z, "LLWEAPONEX_Greatbow_FutureBarrageFire", (INTEGER)_LastTurns, (INTEGER)_NextTurns)
AND
_NextTurns == 1
AND
DB_LLWEAPONEX_Greatbows_Temp_FutureBarrage_LoopFX(_Char, _x, _y, _z, _Handle)
AND
PlayScaledLoopEffectAtPosition("LLWEAPONEX_FX_AreaRadiusDecal_Circle_1m_01", 8.0, _x, _y, _z, _NewHandle) // == 4m
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_FutureBarrage_LoopFX(_Char, _x, _y, _z, _Handle);
DB_LLWEAPONEX_Greatbows_Temp_FutureBarrage_LoopFX(_Char, _x, _y, _z, _NewHandle);
StopLoopEffect(_Handle);

PROC
LeaderLib_Turns_OnTurnCounterComplete_PositionWithObject((STRING)_ID, (GUIDSTRING)_Char, (REAL)_x, (REAL)_y, (REAL)_z, "LLWEAPONEX_Greatbow_FutureBarrageFire")
AND
DB_LLWEAPONEX_Greatbows_Temp_FutureBarrage_LoopFX(_Char, _x, _y, _z, _Handle)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_FutureBarrage_LoopFX(_Char, _x, _y, _z, _Handle);
StopLoopEffect(_Handle);
PlayEffectAtPosition("LLWEAPONEX_FX_Skills_Greatbow_FutureBarrage_Decal_AttackImminent_01", _x, _y, _z);

PROC
LeaderLib_Turns_OnTurnCounterComplete_PositionWithObject((STRING)_ID, (GUIDSTRING)_Char, (REAL)_x, (REAL)_y, (REAL)_z, "LLWEAPONEX_Greatbow_FutureBarrageFire")
AND
GetUUID(_Char, _UUID)
AND
NRD_RealToString(_x, _xs) AND NRD_RealToString(_y, _ys) AND NRD_RealToString(_z, _zs)
THEN
NRD_ModCall("WeaponExpansion", "FutureBarrage_FireDummySkill", _UUID, _xs, _ys, _zs);

IF
CharacterCharacterEvent((CHARACTERGUID)_Caster, (CHARACTERGUID)_Dummy, "LLWEAPONEX_Greatbow_FutureBarrage_DummySkillUsed")
THEN
DB_LLWEAPONEX_Greatbows_Temp_FutureBarrageDummy(_Caster, _Dummy);
LeaderLib_Timers_StartObjectTimer(_Dummy, 4000, "Timers_LLWEAPONEX_Greatbow_FutureBarrage_ClearDummy", "LLWEAPONEX_Greatbow_FutureBarrage_ClearDummy");

IF
StoryEvent((CHARACTERGUID)_Char, "LLWEAPONEX_Greatbow_FutureBarrage_ClearDummy")
AND
DB_LLWEAPONEX_Greatbows_Temp_FutureBarrageDummy(_Caster, _Dummy)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_FutureBarrageDummy(_Caster, _Dummy);
RemoveTemporaryCharacter(_Dummy);

IF
GameStarted(_,_)
AND
DB_LLWEAPONEX_Greatbows_Temp_FutureBarrageDummy(_Caster, _Dummy)
THEN
NOT DB_LLWEAPONEX_Greatbows_Temp_FutureBarrageDummy(_Caster, _Dummy);
RemoveTemporaryCharacter(_Dummy);

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_GREATBOW_FUTUREBARRAGE_DUMMYHIT", (CHARACTERGUID)_Dummy)
AND
DB_LLWEAPONEX_Greatbows_Temp_FutureBarrageDummy(_Caster, _Dummy)
AND
GetUUID(_Caster, _CasterUUID)
AND
GetUUID(_Target, _TargetUUID)
THEN
NRD_ModCall("WeaponExpansion", "FutureBarrage_ApplyDamage", _CasterUUID, _TargetUUID);
CharacterCharacterSetEvent(_Target, _Caster, "LLWEAPONEX_Greatbows_FutureBarrage_CheckMovement"); //Immobile bonus damage check

IF
CharacterStatusApplied(_Target, "LLWEAPONEX_GREATBOW_FUTUREBARRAGE_DUMMYHIT", (CHARACTERGUID)_Caster)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_FutureBarrageDummy(_, _Caster)
AND
NOT DB_LLWEAPONEX_Greatbows_Temp_FutureBarrageDummy(_Caster, _)
AND
GetUUID(_Caster, _CasterUUID)
AND
GetUUID(_Target, _TargetUUID)
THEN
NRD_ModCall("WeaponExpansion", "FutureBarrage_ApplyDamage", _CasterUUID, _TargetUUID);
CharacterCharacterSetEvent(_Target, _Caster, "LLWEAPONEX_Greatbows_FutureBarrage_CheckMovement"); //Immobile bonus damage check

IF
CharacterCharacterEvent(_Caster, _Target, "LLWEAPONEX_Greatbows_FutureBarrage_ApplyBonusDamage")
AND
GetUUID(_Caster, _CasterUUID)
AND
GetUUID(_Target, _TargetUUID)
THEN
NRD_ModCall("WeaponExpansion", "FutureBarrage_ApplyDamage", _CasterUUID, _TargetUUID, "true");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"