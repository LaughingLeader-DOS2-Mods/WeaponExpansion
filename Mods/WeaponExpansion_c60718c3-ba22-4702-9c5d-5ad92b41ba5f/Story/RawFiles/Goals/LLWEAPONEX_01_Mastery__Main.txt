Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLWEAPONEX_WeaponMastery_InitSettings();
KBSECTION

PROC
LLWEAPONEX_WeaponMastery_InitSettings()
THEN
DB_NOOP(1);

//REGION UPDATES
PROC
LeaderLib_Mods_OnVersionChanged(c60718c3-ba22-4702-9c5d-5ad92b41ba5f, (INTEGER)_LastVersion, (INTEGER)_NextVersion)
AND
_LastVersion < 152502272
THEN
/* The great purge. */
//DB_LLWEAPONEX_WeaponMastery_MasteryVariables(_Mastery, _CurrentXPVar, _LevelVar, _EnabledFlag)
SysClear("DB_LLWEAPONEX_WeaponMastery_MasteryVariables", 4);
//DB_LLWEAPONEX_WeaponMastery_MasteryLevelTags(_Mastery, _Level, _UnlockedMasteryLevelTag)
SysClear("DB_LLWEAPONEX_WeaponMastery_MasteryLevelTags", 3);
SysClear("DB_LLWEAPONEX_WeaponMastery_MasteryCap", 1);
SysClear("DB_LLWEAPONEX_WeaponMastery_MaxExperience", 2);
//DB_LLWEAPONEX_WeaponMastery_Progression("LLWEAPONEX_Katana", 1, "Finisher", "Tag");
//DB_LLWEAPONEX_WeaponMastery_Flags("LLWEAPONEX_Katana", "Finisher", "LLWEAPONEX_Katana_FinisherEnabled", "Tag");
SysClear("DB_LLWEAPONEX_WeaponMastery_Progression", 4);
SysClear("DB_LLWEAPONEX_WeaponMastery_Flags", 4);
LLWEAPONEX_WeaponMastery_InitSettings();
//END_REGION

//REGION QUERIES
QRY
LLWEAPONEX_WeaponMastery_QRY_HasMasteryRank((CHARACTERGUID)_Player, (STRING)_Mastery, (INTEGER)_MinLevel)
AND
DB_LLWEAPONEX_WeaponMastery_PlayerData_Experience(_Player, _Mastery, _Level, _Experience)
AND
_Level >= _MinLevel
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_WeaponMastery_QRY_HasActiveMastery((CHARACTERGUID)_Player, (STRING)_Mastery)
AND
DB_LLWEAPONEX_WeaponMastery_Temp_ActiveMasteries(_Player, _Item, _Mastery)
THEN
DB_NOOP(1);

QRY
LLWEAPONEX_WeaponMastery_QRY_HasAnyActiveMastery((CHARACTERGUID)_Player)
AND
DB_LLWEAPONEX_WeaponMastery_Temp_ActiveMasteries(_Player, _Item, _Mastery)
THEN
DB_NOOP(1);
//END_REGION

//REGION ACTIVE_MASTERY_TRACKING
PROC
LLWEAPONEX_WeaponMastery_TrackMastery((CHARACTERGUID)_Player, (ITEMGUID)_Item, (STRING)_Mastery)
AND
NOT DB_LLWEAPONEX_WeaponMastery_Temp_ActiveMasteries(_Player, _, _Mastery)
THEN
LLWEAPONEX_WeaponMastery_OnMasteryActivated(_Player, _Mastery);

PROC
LLWEAPONEX_WeaponMastery_TrackMastery((CHARACTERGUID)_Player, (ITEMGUID)_Item, (STRING)_Mastery)
THEN
DB_LLWEAPONEX_WeaponMastery_Temp_ActiveMasteries(_Player, _Item, _Mastery);

PROC
LLWEAPONEX_WeaponMastery_RemovedTrackedMasteries((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
DB_LLWEAPONEX_WeaponMastery_Temp_ActiveMasteries(_Player, _Item, _Mastery)
THEN
NOT DB_LLWEAPONEX_WeaponMastery_Temp_ActiveMasteries(_Player, _Item, _Mastery);
LLWEAPONEX_WeaponMastery_Internal_CheckRemovedMasteries(_Player, _Mastery);

PROC
LLWEAPONEX_WeaponMastery_Internal_CheckRemovedMasteries((CHARACTERGUID)_Player, (STRING)_Mastery)
AND
NOT DB_LLWEAPONEX_WeaponMastery_Temp_ActiveMasteries(_Player, _, _Mastery)
AND
IsTagged(_Player, _Mastery, 1)
THEN
ClearTag(_Player, _Mastery);
LLWEAPONEX_WeaponMastery_OnMasteryDeactivated(_Player, _Mastery);
//END_REGION

//REGION ACTIVATION_DEACTIVATION
PROC
LLWEAPONEX_WeaponMastery_OnMasteryActivated((CHARACTERGUID)_Player, (STRING)_Mastery)
AND
NOT DB_GlobalFlag("LLWEAPONEX_MasterySystemActive")
THEN
LeaderLib_ToggleScripts_EnableScript("LLWEAPONEX_MasterySystemActive", "WeaponExpansion");

PROC
LLWEAPONEX_WeaponMastery_OnMasteryDeactivated((CHARACTERGUID)_Player, (STRING)_Mastery)
AND
NOT DB_LLWEAPONEX_WeaponMastery_Temp_ActiveMasteries(_Player, _, _Mastery)
THEN
NRD_ModCall("WeaponExpansion", "OnMasteryDeactivated", (STRING)_Player, _Mastery);

PROC
LLWEAPONEX_WeaponMastery_OnMasteryDeactivated((CHARACTERGUID)_Player, (STRING)_Mastery)
AND
NOT DB_LLWEAPONEX_WeaponMastery_Temp_ActiveMasteries(_,_,_)
AND
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _Mastery, _IsTag)
THEN
LeaderLib_ToggleScripts_DisableScriptAfterDelay("LLWEAPONEX_MasterySystemActive", "WeaponExpansion", 500);
//END_REGION

//REGION TOGGLE_SCRIPTS
PROC
LLWEAPONEX_WeaponMastery_OnMasteryActivated((CHARACTERGUID)_Player, (STRING)_Mastery)
AND
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _Mastery, _IsTag)
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Player, _Mastery, "WeaponExpansion", 0);

PROC
LLWEAPONEX_WeaponMastery_OnMasteryDeactivated((CHARACTERGUID)_Player, (STRING)_Mastery)
AND
DB_LeaderLib_ToggleScripts_Registered(_GoalTitle, _Mastery, _IsTag)
THEN
LeaderLib_ToggleScripts_DisableScriptForObject(_Player, _Mastery, "WeaponExpansion", 0);
//END_REGION

//REGION UNEQUIP_REMOVAL_EVENT
//Delay removal in case the same weapon type is being equipped
PROC
LLWEAPONEX_Equipment_OnTaggedItemUnEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Tag, (INTEGER)_IsPlayer)
AND
DB_LLWEAPONEX_WeaponMastery_Temp_ActiveMasteries(_Player, _Item, _Tag)
THEN
DB_LLWEAPONEX_WeaponMastery_Temp_DeactivatedMasteries(_Player, _Item, _Tag);
LeaderLib_Timers_StartObjectTimer(_Player, 1000, "Timers_LLWEAPONEX_WeaponMastery_MasteryDeactivated", "LLWEAPONEX_WeaponMastery_MasteryDeactivated");

PROC
LLWEAPONEX_Equipment_OnTaggedItemEquipped((CHARACTERGUID)_Char, (ITEMGUID)_Item, (STRING)_Tag, (INTEGER)_IsPlayer)
AND
DB_LLWEAPONEX_WeaponMastery_Temp_DeactivatedMasteries(_Player, _Item, _Tag)
THEN
NOT DB_LLWEAPONEX_WeaponMastery_Temp_DeactivatedMasteries(_Player, _Item, _Tag);
LeaderLog_Log("DEBUG", "[LLWEAPONEX_01_Mastery__Main:LLWEAPONEX_Equipment_OnTaggedItemEquipped] Weapon Mastery [",_Tag,"] was reactivated. Removing from [DB_LLWEAPONEX_WeaponMastery_Temp_DeactivatedMasteries].");

IF
StoryEvent((CHARACTERGUID)_Player, "LLWEAPONEX_WeaponMastery_MasteryDeactivated")
AND
DB_LLWEAPONEX_WeaponMastery_Temp_DeactivatedMasteries(_Player, _Item, _Mastery)
AND
NOT LLWEAPONEX_WeaponMastery_QRY_HasActiveMastery(_Player, _Mastery)
THEN
NOT DB_LLWEAPONEX_WeaponMastery_Temp_DeactivatedMasteries(_Player, _Item, _Mastery);
LeaderLog_Log("DEBUG", "[LLWEAPONEX:WeaponMastery:LLWEAPONEX_Timers_MasteryDeactivated] Weapon Mastery [",_Mastery,"] was deactivated.");
LLWEAPONEX_WeaponMastery_OnMasteryDeactivated(_Player, _Mastery);

IF
StoryEvent((CHARACTERGUID)_Player, "LLWEAPONEX_WeaponMastery_MasteryDeactivated")
AND
DB_LLWEAPONEX_WeaponMastery_Temp_DeactivatedMasteries(_Player, _Item, _Mastery)
THEN
NOT DB_LLWEAPONEX_WeaponMastery_Temp_DeactivatedMasteries(_Player, _Item, _Mastery);
//END_REGION

//REGION STORE_XP
PROC
LLWEAPONEX_WeaponMastery_Internal_StoreExperience((CHARACTERGUID)_Player, (STRING)_Mastery, (INTEGER)_Level, (INTEGER)_TotalExperience)
AND
DB_LLWEAPONEX_WeaponMastery_PlayerData_Experience(_Player, _Mastery, _LastLevel, _LastExperience)
THEN
NOT DB_LLWEAPONEX_WeaponMastery_PlayerData_Experience(_Player, _Mastery, _LastLevel, _LastExperience);

PROC
LLWEAPONEX_WeaponMastery_Internal_StoreExperience((CHARACTERGUID)_Player, (STRING)_Mastery, (INTEGER)_Level, (INTEGER)_TotalExperience)
THEN
DB_LLWEAPONEX_WeaponMastery_PlayerData_Experience(_Player, _Mastery, _Level, _TotalExperience);
//END_REGION

//REGION STATUS_UNLOCKING
PROC
LLWEAPONEX_WeaponMastery_ApplyInfoStatus((CHARACTERGUID)_Player, (STRING)_Status, (STRING)_InfoStatus, 0)
AND
_InfoStatus != ""
AND
HasActiveStatus(_Player, _InfoStatus, 0)
AND
GetStatusTurns(_Player, _Status, _Turns)
AND
Real(_Turns, _TurnsR)
AND
RealProduct(_TurnsR, 6.0, _Duration)
THEN
ApplyStatus(_Player, _InfoStatus, _Duration, 1, _Player);

PROC
LLWEAPONEX_WeaponMastery_ApplyInfoStatus((CHARACTERGUID)_Player, (STRING)_Status, (STRING)_InfoStatus, 1)
AND
_InfoStatus != ""
AND
GetStatusTurns(_Player, _Status, _Turns)
AND
Real(_Turns, _TurnsR)
AND
RealProduct(_TurnsR, 6.0, _Duration)
THEN
ApplyStatus(_Player, _InfoStatus, _Duration, 1, _Player);

/*
QRY
LLWEAPONEX_WeaponMastery_QRY_HasAnySkillUnlockStatus((CHARACTERGUID)_Player, (STRING)_Mastery, (STRING)_SkillID)
AND
DB_LLWEAPONEX_WeaponMastery_StatusUnlocking(_Mastery, _SkillID, _Status, _InfoStatus)
AND
HasActiveStatus(_Player, _Status, 1)
THEN
DB_NOOP(1);
*/

QRY
LLWEAPONEX_WeaponMastery_QRY_RemoveInfoStatus((CHARACTERGUID)_Player, (STRING)_Mastery, (STRING)_SkillID, (STRING)_InfoStatus)
THEN
LLWEAPONEX_WeaponMastery_RemoveInfoStatus(_Player, _Mastery, _SkillID, _InfoStatus);

PROC
LLWEAPONEX_WeaponMastery_RemoveInfoStatus((CHARACTERGUID)_Player, (STRING)_Mastery, (STRING)_SkillID, (STRING)_InfoStatus)
AND
_InfoStatus != ""
//AND
//NOT LLWEAPONEX_WeaponMastery_QRY_HasAnySkillUnlockStatus(_Player, _Mastery, _SkillID)
THEN
RemoveStatus(_Player, _InfoStatus);
//END_REGION

IF
StoryEvent(_Char, "LLWEAPONEX_ApplyHasted")
THEN
ApplyStatus(_Char, "HASTED", 6.0, 0, _Char);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_00_PostCC"