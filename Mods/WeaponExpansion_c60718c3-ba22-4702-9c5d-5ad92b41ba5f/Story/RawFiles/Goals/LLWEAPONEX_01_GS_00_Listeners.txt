Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

//REGION PRESET_APPLIED
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "LeaderLib_Timers_PresetMenu_OnPresetApplied")
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Char)
AND
DB_LeaderLib_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Char, _Slot,  (ITEMGUID)_Item)
AND
_Item != NULL_00000000-0000-0000-0000-000000000000
AND
NOT LeaderLib_Helper_QRY_IgnoreItem(_Item)
AND
GetTemplate(_Item, _Template)
THEN
LLWEAPONEX_OnItemTemplateEquipped(_Char, _Item, _Template);
//END_REGION

//REGION SKILL_TYPE_USAGE
IF
CharacterUsedSkill(_Char, _Skill, _SkillType, _Element)
AND
GetUUID(_Char, _UUID)
THEN
NRD_ModCall("WeaponExpansion", "OnSkillEvent", _UUID, "USED", _Skill, _SkillType, _Element);

IF
SkillCast(_Char, _Skill, _SkillType, _Element)
AND
GetUUID(_Char, _UUID)
THEN
NRD_ModCall("WeaponExpansion", "OnSkillEvent", _UUID, "CAST", _Skill, _SkillType, _Element);
//END_REGION

//REGION TAG_REQUIREMENTS_CHANGED
IF
ObjectWasTagged((CHARACTERGUID)_Char, "LLWEAPONEX_NoMeleeWeaponEquipped")
AND
CharacterIsPlayer(_Char, 1)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 250, "Timers_LLWEAPONEX_RefreshRequirementTagSkills", "LLWEAPONEX_RefreshRequirementTagSkills");

IF
ObjectLostTag((CHARACTERGUID)_Char, "LLWEAPONEX_NoMeleeWeaponEquipped")
AND
CharacterIsPlayer(_Char, 1)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 250, "Timers_LLWEAPONEX_RefreshRequirementTagSkills", "LLWEAPONEX_RefreshRequirementTagSkills");

IF
ObjectWasTagged((CHARACTERGUID)_Char, "LLWEAPONEX_CannotUseScoundrelSkills")
AND
CharacterIsPlayer(_Char, 1)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 250, "Timers_LLWEAPONEX_RefreshRequirementTagSkills", "LLWEAPONEX_RefreshRequirementTagSkills");

IF
ObjectLostTag((CHARACTERGUID)_Char, "LLWEAPONEX_CannotUseScoundrelSkills")
AND
CharacterIsPlayer(_Char, 1)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 250, "Timers_LLWEAPONEX_RefreshRequirementTagSkills", "LLWEAPONEX_RefreshRequirementTagSkills");

IF
StoryEvent(_Char, "LLWEAPONEX_RefreshRequirementTagSkills")
AND
GetUUID(_Char, _UUID)
THEN
NRD_ModCall("LeaderLib", "RefreshSkillBar", _UUID);
//END_REGION

//REGION CLEANUP
IF
GameStarted(_Region, _)
AND
IsGameLevel(_Region, 1)
AND
DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item, _WeaponType)
AND
ObjectExists(_Char, _a)
AND
ObjectExists(_Item, _b)
AND
LeaderLib_Math_QRY_IsEqualToAny(0, _a, _b)
AND
GetTemplate(_Item, _Template)
THEN
NOT DB_LLWEAPONEX_Equipment_TrackedItems(_Char, _Item, _WeaponType);
LLWEAPONEX_Equipment_Internal_CleanupEntry(_Char, _Item, _a, _b);
//END_REGION
EXITSECTION
ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_03_GameScripts"