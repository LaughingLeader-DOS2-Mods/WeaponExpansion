Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_LLWEAPONEX_CC_TemplateEffects("WPN_LLWEAPONEX_CombatShield_Blackring_1H_A_ec353f1e-c1ca-46d1-83ef-e9f4fea14475", "LLWEAPONEX_FX_EQ_CombatShield_Blackring_B");
DB_LLWEAPONEX_CC_TemplateEffects("WPN_LLWEAPONEX_CombatShield_Common_1H_A_8c7da07b-ad11-4a0c-8406-0261977042b6", "LLWEAPONEX_FX_EQ_CombatShield_Common_B");
DB_LLWEAPONEX_CC_TemplateEffects("WPN_LLWEAPONEX_CombatShield_Dwarves_1H_A_bc034226-19bd-45e6-be7d-ec0d28c2e412", "LLWEAPONEX_FX_EQ_CombatShield_Dwarves_B");
DB_LLWEAPONEX_CC_TemplateEffects("WPN_LLWEAPONEX_CombatShield_Elves_1H_B_1268f5f0-e484-42ea-8c13-0014e6aeaaad", "LLWEAPONEX_FX_EQ_CombatShield_Elves_A");
DB_LLWEAPONEX_CC_TemplateEffects("WPN_LLWEAPONEX_CombatShield_Humans_1H_A_3a404dab-4862-4490-aa0f-bc27d06fdc6c", "LLWEAPONEX_FX_EQ_CombatShield_Common_B");
DB_LLWEAPONEX_CC_TemplateEffects("WPN_LLWEAPONEX_CombatShield_Lizards_1H_C_067f48be-857d-43a8-bd0a-add59f025843", "LLWEAPONEX_FX_EQ_CombatShield_Lizards_D");

DB_LLWEAPONEX_CC_OriginPreset("LLWEAPONEX_Korvash");
DB_LLWEAPONEX_CC_OriginPreset("LLWEAPONEX_Harken");
KBSECTION

IF
RegionStarted(_Level)
AND
IsGameLevel(_Level, 1)
THEN
GoalCompleted;

//REGION VISUAL_FIX
/*
IF
SkillAdded(_Char, _Skill, 0)
AND
CharacterGetOrigin(_Char, 1, "LLWEAPONEX_Harken")
THEN
CharacterSetVisualElement(_Char, 0, "Hair");
CharacterSetVisualElement(_Char, 0, "HairColor");
CharacterSetVisualElement(_Char, 0, "Color");
*/

IF
SkillAdded(_Char, _Skill, _)
AND
NOT DB_LLWEAPONEX_CC_JustReactedToSkills(_Char)
AND
CharacterGetOrigin(_Char, 1, _Preset)
AND
GetUUID(_Char, _UUID)
THEN
DB_LLWEAPONEX_CC_JustReactedToSkills(_Char);
ProcObjectTimer(_Char, "Timers_LLWEAPONEX_CC_ClearBlockerDB", 50);
NRD_ModCall("WeaponExpansion", "CC_OnPresetSelected", _UUID, _Preset);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "Timers_LLWEAPONEX_CC_ClearBlockerDB")
AND
DB_LLWEAPONEX_CC_JustReactedToSkills(_Char)
THEN
NOT DB_LLWEAPONEX_CC_JustReactedToSkills(_Char);
//END_REGION

//REGION CC_EFFECTS
IF
ItemTemplateEquipped(_Template, _Player)
AND
DB_LLWEAPONEX_CC_TemplateEffects(_Template, _Effect)
AND
PlayLoopEffect(_Player, _Effect, "LowerArm_R_Twist_Bone", _Handle)
THEN
DB_LLWEAPONEX_CC_Temp_LoopEffects(_Player, _Template, _Handle);

IF
ItemTemplateUnEquipped(_Template, _Player)
AND
DB_LLWEAPONEX_CC_Temp_LoopEffects(_Player, _Template, _Handle)
THEN
NOT DB_LLWEAPONEX_CC_Temp_LoopEffects(_Player, _Template, _Handle);
StopLoopEffect(_Handle);

PROC
LeaderLib_CC_OnPresetChanged((CHARACTERGUID)_Player, (STRING)_LastPreset, "LLWEAPONEX_Pirate")
AND
IsTagged(_Player, "FEMALE", 1)
AND
IsTagged(_Player, "DWARF", 1)
AND
CharacterGetEquippedWeapon(_Player, (ITEMGUID)_Weapon)
AND
NOT GetTemplate(_Weapon, "WPN_LLWEAPONEX_Placeholder_NoVisual_eb18b205-78a5-4e0c-8c4a-63ac748b07cf")
AND
GetPosition(_Player, _x, _y, _z)
AND
CreateItemTemplateAtPosition("WPN_LLWEAPONEX_Placeholder_NoVisual_eb18b205-78a5-4e0c-8c4a-63ac748b07cf", _x, _y, _z, _Placeholder)
THEN
CharacterUnequipItem(_Player, _Weapon);
LeaderLib_Helper_EquipInSlot(_Player, _Placeholder, "Weapon");
LeaderLib_Helper_EquipInSlot(_Player, _Weapon, "Shield");

PROC
LeaderLib_CC_OnPresetChanged((CHARACTERGUID)_Player, (STRING)_LastPreset, (STRING)_Preset)
AND
CharacterGetOrigin(_Player, 1, "LLWEAPONEX_Harken")
THEN
NRD_ModCall("WeaponExpansion", "CC_SwapToHarkenAnvilPreview", (STRING)_Player, _Preset);

IF
RegionEnded(_Level)
AND
IsCharacterCreationLevel(_Level, 1)
AND
DB_LLWEAPONEX_CC_Temp_LoopEffects(_Player, _Template, _Handle)
THEN
NOT DB_LLWEAPONEX_CC_Temp_LoopEffects(_Player, _Template, _Handle);
StopLoopEffect(_Handle);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_WeaponExpansion"