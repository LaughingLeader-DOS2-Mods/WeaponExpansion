Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

IF
RegionStarted(_Level)
AND
IsGameLevel(_Level, 1)
THEN
GoalCompleted;

//REGION VISUAL_FIX
IF
SkillAdded(_Char, _Skill, _)
AND
NOT DB_LLWEAPONEX_CC_JustReactedToSkills(_Char)
AND
CharacterGetOrigin(_Char, 1, _Preset)
AND
GetUUID(_Char, _UUID)
THEN
DB_LLWEAPONEX_CC_JustReactedToSkills(_Char);
ProcObjectTimer(_Char, "Timers_LLWEAPONEX_CC_ClearBlockerDB", 50);
NRD_ModCall("WeaponExpansion", "CC_OnPresetSelected", _UUID, _Preset);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "Timers_LLWEAPONEX_CC_ClearBlockerDB")
AND
DB_LLWEAPONEX_CC_JustReactedToSkills(_Char)
THEN
NOT DB_LLWEAPONEX_CC_JustReactedToSkills(_Char);
//END_REGION

//REGION CC_EFFECTS
PROC
LeaderLib_CC_OnPresetChanged((CHARACTERGUID)_Player, (STRING)_LastPreset, "LLWEAPONEX_Pirate")
AND
IsTagged(_Player, "FEMALE", 1)
AND
IsTagged(_Player, "DWARF", 1)
AND
CharacterGetEquippedWeapon(_Player, (ITEMGUID)_Weapon)
AND
NOT GetTemplate(_Weapon, "WPN_LLWEAPONEX_Placeholder_NoVisual_eb18b205-78a5-4e0c-8c4a-63ac748b07cf")
AND
GetPosition(_Player, _x, _y, _z)
AND
CreateItemTemplateAtPosition("WPN_LLWEAPONEX_Placeholder_NoVisual_eb18b205-78a5-4e0c-8c4a-63ac748b07cf", _x, _y, _z, _Placeholder)
THEN
CharacterUnequipItem(_Player, _Weapon);
LeaderLib_Helper_EquipInSlot(_Player, _Placeholder, "Weapon");
LeaderLib_Helper_EquipInSlot(_Player, _Weapon, "Shield");

PROC
LeaderLib_CC_OnPresetChanged((CHARACTERGUID)_Player, (STRING)_LastPreset, (STRING)_Preset)
AND
CharacterGetOrigin(_Player, 1, "LLWEAPONEX_Harken")
THEN
NRD_ModCall("WeaponExpansion", "CC_SwapToHarkenAnvilPreview", (STRING)_Player, _Preset);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_WeaponExpansion"