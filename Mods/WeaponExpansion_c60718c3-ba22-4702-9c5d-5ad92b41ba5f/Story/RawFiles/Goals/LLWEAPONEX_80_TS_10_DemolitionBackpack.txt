Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

//REGION CHARGES
IF
CharacterPrecogDying(_Enemy)
AND
DB_LLWEAPONEX_Demolition_PlayerData_BackpackEquipped(_Character, _Item)
AND
_Character != _Enemy
AND
NOT LeaderLib_Helper_QRY_ChargesAreMaxed(_Item)
AND
CharacterIsSummon(_Enemy, 0)
AND
CharacterIsEnemy(_Enemy, _Character, 1)
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Enemy)
THEN
ItemAddCharges(_Item, 1);

IF
CharacterKilledBy(_Enemy, _, _Player)
AND
DB_LLWEAPONEX_RemoteMines_Temp_JustDetonated(_Player)
THEN
CharacterGiveReward(_Player, "ST_LLWEAPONEX_FreeGrenades", 1);
LLWEAPONEX_Demolition_AddCharge(_Player);
ProcObjectTimerCancel(_Player, "LLWEAPONEX_Timers_RemoteMines_ResetJustDetonated");
ProcObjectTimer(_Player, "LLWEAPONEX_Timers_RemoteMines_ResetJustDetonated", 500);
//END_REGION

//REGION ENHANCED_DEMOLITION
PROC
ProcProcessUseOfItem((CHARACTERGUID)_Player, (ITEMGUID)_Item, (INTEGER)_RequestID)
AND
NOT DB_CustomUseItemResponse(_Player, _Item, 0)
AND
DB_IsPlayer(_Player)
AND
LLWEAPONEX_Demolition_QRY_IsBonusItem(_Item)
AND
HasActiveStatus(_Player, "LLWEAPONEX_BACKPACK_DEMOLITION_EQUIPPED", 1)
THEN
CharacterItemSetEvent(_Player, _Item, "LLWEAPONEX_ItemCheck_FindSkill");

IF
CharacterItemEvent(_Player, _Item, "LLWEAPONEX_ItemCheck_HasSkill")
AND
GetVarString(_Item, "LLWEAPONEX_ItemSkill", _Skill)
AND
LLWEAPONEX_Demolition_QRY_SkillHasBonuses(_Skill)
THEN
DB_LLWEAPONEX_Demolition_Temp_ListenForSkill(_Player, _Item, _Skill);
LeaderLog_Log("DEBUG", "[LLWEAPONEX_80_TS_10_DemolitionBackpack:LLWEAPONEX_ItemCheck_HasSkill] Listening for item skill [",_Skill,"].");

//Cancelled
IF
CharacterStoppedUsingItem(_Player, _Item)
AND
DB_LLWEAPONEX_Demolition_Temp_ListenForSkill(_Player, _Item, _Skill)
THEN
NOT DB_LLWEAPONEX_Demolition_Temp_ListenForSkill(_Player, _Item, _Skill);
LLWEAPONEX_Demolition_ClearListeningEntries(_Player);
LeaderLog_Log("DEBUG", "[LLWEAPONEX_80_TS_10_DemolitionBackpack:CharacterStoppedUsingItem] Item usage cancelled.");

IF
CharacterUsedSkillOnTarget(_Player, _Target, _Skill, _, _)
AND
DB_LLWEAPONEX_Demolition_Temp_ListenForSkill(_Player, _Item, _Skill)
THEN
NOT DB_LLWEAPONEX_Demolition_Temp_ListenForSkill(_Player, _Item, _Skill);
DB_LLWEAPONEX_Demolition_Temp_ListenForSkill(_Player, _Target, _Item, _Skill);
LeaderLog_Log("DEBUG", "[LLWEAPONEX_80_TS_10_DemolitionBackpack:CharacterUsedSkillOnTarget] Skill [",_Skill,"] used on target.");

IF
CharacterUsedSkillAtPosition(_Player, _x, _y, _z, _Skill, _, _)
AND
DB_LLWEAPONEX_Demolition_Temp_ListenForSkill(_Player, _Item, _Skill)
THEN
DB_LLWEAPONEX_Demolition_Temp_ListenForSkillPosition(_Player, _Item, _Skill, _x, _y, _z);

IF
SkillCast(_Player, _Skill, _, _)
AND
LLWEAPONEX_Demolition_QRY_ListeningForSkill(_Player, _Skill)
AND
LeaderLib_Random_QRY(900, 1400)
AND
DB_LeaderLib_Random(_Delay)
THEN
NOT DB_LeaderLib_Random(_Delay);
LLWEAPONEX_Demolition_OnItemSkillCast(_Player, _Skill);
LeaderLib_Timers_StartObjectTimer(_Player, _Delay, "LLWEAPONEX_Timers_Demolition_ApplySkillBonus", "LLWEAPONEX_Demolition_ApplySkillBonus");
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLWEAPONEX_80_ToggledScripts"